######################################
#  Adventuron                        #
######################################

game_information {
   game_name                    = CC's Road to Stardom
   game_version                 = 1.0.0
   game_shortname               = CC Star
   written_by                   = OK Feather
   uuid                         = 910e5474-beef-11ec-9d64-0242ac120002
   year_of_release              = 2022
   short_synopsis               = Help 50% cat, 50% bunny, 100% cute CC become an online idol!
}

themes {
  theme_base : theme {
      colors {
         exit_list_item_pen = #fdd
      }
      lister_exits {
         is_list_enter = true
      }
  }
  theme_mono : theme {
      theme_settings {
         font = daad
      }
   }
}

######################################
#  Game Settings                     #
######################################

game_settings {
   auto_ask_sound_permissions = false
   enable_standard_all_behaviour = false
}

loading_screen = loading_screen
redescribe = auto_beta

start_at = cc_room

vocabulary {
   : verb / aliases = [examine, check, regarde, touch]
   : verb / aliases = [give, show]
   : verb / aliases = [up, upstair, upstairs]
   : verb / aliases = [down, downstair, downstairs]
   : verb / aliases = [talk, say, speak, tell, ask, wave]
   : verb / aliases = [comment, comments, note, notes]
   : verb / aliases = [exchange, exchanging]
   : verb / aliases = [exit, leave, outside, out]
   : verb / aliases = [enter, inside, in]
   : verb / aliases = [ring,strike]
   : verb / aliases = [thank, thanks]
   : verb / aliases = [happy, merry, feliz, joyous, feathery, great, joyeux, enjoy, warmest, jolly, cheerful, 행복한, 메리, meli]
   : verb / aliases = [kill,threaten,menace,intimidate,browbeat,bully,scare,frighten,terrorize,murder,assassinate,beat,fight,combat,hurt,kick,harm,injure,insult]
   : verb / aliases = ["cry", ":(","=(", "='(", ":'("]
   : verb / aliases = ["smile", "grin", "laugh", ":)","=)",":D","=D"]
   : verb / aliases = [unlock, password, pass, type, write, input]
   : noun      / aliases = [bread, croissant, pain, cookie, loaf, biscuit]
   : noun      / aliases = [cafe, coocoo, shop, restaurant]
   : noun      / aliases = [owner, vendor]
   : noun      / aliases = [pigeon]
   : noun      / aliases = [child, children, kid]
   : noun      / aliases = [comment, note]
   : noun      / aliases = [bird, dove]
   : noun      / aliases = [mirror, glass, reflection]
   : noun      / aliases = [garden, park, yard, lawn, land, plot]
   : noun      / aliases = [fight, combat, conflict]
   : noun      / aliases = [floor, ground, flooring]
   : noun      / aliases = [up, upstair, upstairs]
   : noun      / aliases = [laptop, computer]
   : noun      / aliases = [screen, display]
   : noun      / aliases = [porthole, window]
   : noun      / aliases = [snow, white, snowwhite]
   : noun      / aliases = [plush, plushie, stuffedanimal, animal, stuffed, lion, bear, teddy, teddybear, stuffie, plushtoy, stuffy]
   : noun      / aliases = [boat, ship, barge, gondola, yacht, sailboat, ark, bateau]
   : noun      / aliases = [python, pighon]
   : noun      / aliases = [tree, pine, forest]
   : noun      / aliases = [seven, 7, 7C, 7Cs, sevenseas, daphne, robot]
   : noun      / aliases = [rud, ru, duri, dhxp, gamer]
   : noun      / aliases = [wool, woolion, mechanic, lion]
   : noun      / aliases = [june, inventor, jjyy]
   : noun      / aliases = [florrie, flora]
   : noun      / aliases = [e, ee, etana]
   : noun      / aliases = [yourself, cc, myself, me]
}

######################################
#  Variables                         #
######################################

booleans {
   can_leave_crispy_garden : boolean "false";
   help_about_commands : boolean "false";
   is_sound_muted : boolean "true";
   music_ever_enabled : boolean "false";
   solved_wool_riddle : boolean "false";
   can_create_june_e : boolean "false";
   is_seven_seas_crispy : boolean "true";
   is_wool_wheeled : boolean "true";
   is_laptop_unlocked : boolean "false";
   is_rud_sad : boolean "true" ;
   is_hero_cc : boolean "false" ;
   is_everybody_here : boolean "false" ;
   is_june_in_living_room : boolean "false" ;
   is_e_in_living_room : boolean "false" ;
   is_florrie_in_living_room : boolean "false" ;
   is_florrie_in_living_room_with_cake : boolean "false" ;
}

integers {
   pitch_random : integer "0";
}

strings {
   next_track : string "";
   current_track : string "";
   sound_volume : string "normal";
   string_tmp : string "";
   comments : string "";
   nothing_message : string "";
   string_correction : string "";

}

######################################
#  Locations                         #
######################################

locations {

   cc_room : location "Hello CC! This is your bedroom, typical of rooms in spaceships nowadays. Today is your birthday, so your inventor friend <June<#fee>> made a custom magic <mirror<#fee>> that answers your questions. Feel free to <TALK<#fee>> to the mirror." ;

   cc_hero_june : location "June helped you transform into HeroCC." ;

   cc_win : location "Everybody is here, finally we can eat cake!" ;

   crispy_garden : location "You are in the Crispy Garden. You see trees where <chicken wings<#fee>> grow.";

   florrie_laptop : location "You are in front of Florrie's laptop. Can you <UNLOCK<#fee>> or <TYPE<#fee>> her password?";

   living_room : location "You are in the common living room." ;

   party_planet : location "You are in front of Party Game Planet, the gaming room. Maybe you should ENTER?" ;

   party_planet_inside : location "You are inside the gaming room. The customers here are regulars." ;

   wools_wheels : location "You are at Wool's Wheels, <Wool<#fee>>'s vehicle and ship repair area." ;

}

######################################
#  Connections                       #
######################################

connections {

   from, direction, to = [
   cc_room, south, living_room,
   living_room, west, wools_wheels,
   wools_wheels, south, party_planet,
   party_planet, enter, party_planet_inside,
   party_planet, east, crispy_garden,
   crispy_garden, north, living_room,
   ]

}

######################################
#  Objects                           #
######################################

objects {

chicken_tree : object "a typical <chicken tree<#fee>>" at = "crispy_garden" ;

ee : scenery "<pigeon E<#fee>>, a friend of June who made a few viral videos" ;

june : scenery "inventor <June<#fee>>, who gifted the custom <mirror<#fee>> to CC" ;

laptop : scenery "a <laptop<#fee>> computer" at = "living_room";

map : object "a <map<#fdd>> of the ship" at = "cc_room";

mirror : object "the <mirror<#fee>> June made for you" at = "cc_room";

rud_happy : scenery "<RuD<#fee>>, who thanks to you, can finish the game then watch a Let's Play video";

rud_sad : scenery "<RuD<#fee>>, who is almost always there" at = "party_planet_inside";

seven_seas : scenery "one of the most data savvy robots, called <Seven Seas<#fee>> or <7Cs<#fee>>" ;

wool : scenery "mechanic <Wool<#fee>>, who does all kinds of repair work here" at = "wools_wheels";

}

######################################
#  Barriers                          #
######################################

barriers {
   block_laptop_wools_wheels : block_path {
      from = living_room
      to = wools_wheels
      message  = Curiosity makes your mind more active and open to new ideas. That's why health professionals would recommend unlocking this computer for your personal growth.
      block_when_not = is_laptop_unlocked

   }

   block_wool : block_path {
      from = wools_wheels
      to = party_planet
      message  = Any internet superstar worthy of the name had to solve a certain number of puzzles first.
      block_when_not = solved_wool_riddle
      show_blocked_exit = true

   }

   block_leave_party_planet : block_path {
      from = party_planet_inside
      to = party_planet
      message  = Please talk to RuD and show some compassion, he has to finish his game and cannot lose face.
      block_when = is_rud_sad
      show_blocked_exit = true

   }

   block_rud_sad : block_path {

      from = party_planet
      to = crispy_garden
      message  = It's early in the morning, the garden is still closed.
      block_when = is_rud_sad
      show_blocked_exit = true

   }

   block_seven_seas : block_path {
      from = crispy_garden
      to = living_room
      message  = You should enjoy the view a little longer.
      block_when_not = can_leave_crispy_garden
      show_blocked_exit = true
   }

block_living_room_to_crispy_garden : block_path {
      from = living_room
      to = crispy_garden
      message  = It's early in the morning, let's stay here for a bit.
      block_when_not_exists = seven_seas
      show_blocked_exit = true
   }
}

######################################
#  On Startup                        #
######################################

on_startup {
   : if (!sysvar_bool "sysvar_has_asked_sound") {
      : print "CC's Road to Stardom uses background music.\n\nYou can lower the volume or stop the music at any moment using <SOUND LOWEST<#fdd>> or <SOUND OFF<#fdd>>.\n\nDo you wish to enable the music?\n";
      : gosub "set_sound_on";
      : if (!sysvar_bool "sysvar_music_enabled") {
         : print "You can enable the music at any moment with <SOUND ON<#fdd>>.";
         : press_any_key;
      }
      : clear_screen;
   }
   : else_if (sysvar_bool "sysvar_music_enabled") {
      : set_true "music_ever_enabled";
      : if (sysvar_bool "sysvar_sound_enabled") {
         : set_false "is_sound_muted";
         : print "You can lower or stop the sound at any moment with <SOUND LOWEST<#fdd>> or <SOUND OFF<#fdd>>.\n";
         : press_any_key;
      }
      : else {
         : print "CC's Road to Stardom uses background music. You can enable the music at any moment with <SOUND ON<#fdd>> or <SOUND LOWEST<#fdd>> for lower volume.\n";
         : press_any_key;
      }
   }
   : gosub "update_volume";
   : play_music "music_beat_2";
   : print "Type <VERB NOUN<#fdd>> with the keyboard to advance in the game." ;
   : if (is_mobile()) {
      : print "You can touch some coloured words to copy them, or touch and hold to choose an action.";
   }
   : else {
      : print "You can click some coloured words to copy them, or right-click to choose an action.";
   }
   : print "e.g. <EXAMINE PIGEON<#fdd>> (or <X<#fdd>>) to take a closer look at a PIGEON." ;
   : print "<GET CHICKEN<#fdd>> to take CHICKEN and add it to your inventory." ;
   : press_any_key ;
   : print "To travel, type <NORTH<#fdd>>, <EAST<#fdd>>, <SOUTH<#fdd>>, <WEST<#fdd>> (or <N<#fdd>>, <E<#fdd>>, <S<#fdd>>, <W<#fdd>>), <ENTER<#fdd>>, <EXIT<#fdd>>, <UP<#fdd>>, <DOWN<#fdd>>." ;
   : press_any_key ;
   : print "To check your inventory, type <INVENTORY<#fdd>> (or <I<#fdd>>)." ;
   : press_any_key ;
   : print "To see available commands at a location, type <HELP<#fdd>>." ;
   : press_any_key ;

}

######################################
#  Subroutines                       #
######################################

subroutines {
   set_sound_on : subroutine {
      : if (!sysvar_bool "sysvar_sound_enabled") {
         : ask_permission ask_again_and_again = "true" category = "sound" ;
      }
      : if (sysvar_bool "sysvar_sound_enabled" && !sysvar_bool "sysvar_music_enabled") {
            : ask_permission ask_again_and_again = "true" category = "music" ;
      }
      : if (sysvar_bool "sysvar_music_enabled") {
         : set_false "is_sound_muted";
         : gosub "update_volume";
         : set_true "music_ever_enabled";
      }
   }
   random_music : subroutine {
      : if (is_sound_muted) {
         : return;
      }
      : store_random var = "pitch_random";
      : if (pitch_random < 20) {
        :  beep millis="200" pitch="30";
      }
      : else_if (pitch_random < 40) {
        :  beep millis="200" pitch="33";
      }
      : else_if (pitch_random < 60) {
        :  beep millis="200" pitch="36";
      }
      : else_if (pitch_random < 80) {
        :  beep millis="200" pitch="39";
      }
      : else {
        :  beep millis="200" pitch="42";
      }
   }
   play_music : subroutine {
      : if (is_sound_muted) {
         : return;
      }
      : if (is_at "cc_room") {
         : set_string var="next_track" text="music_beat_2";
         : if (next_track != current_track) {
            : play_music "music_beat_2";
         }
      }
      : else_if (is_at "party_planet" || is_at "florrie_laptop") {
         : set_string var="next_track" text="music_short";
         : if (next_track != current_track) {
            : if (chance(50)) {
               : play_music "music_living_room";
            }
            : else {
               : play_music "music_wools_wheels";
            }
         }
      }
      : else_if (is_at "wools_wheels") {
         : set_string var="next_track" text="music_beat_1";
         : if (next_track != current_track) {
            : play_music "music_beat_1";
         }
      }
      : else_if (is_at "crispy_garden") {
         : set_string var="next_track" text="music_crispy_garden";
         : if (next_track != current_track) {
            : play_music "music_crispy_garden";
         }
      }
      : else_if (is_at "living_room") {
         : set_string var="next_track" text="music_living_room";
         : if (next_track != current_track) {
            : play_music "music_living_room";
         }
      }
      : else_if (is_at "party_planet_inside") {
         : set_string var="next_track" text="music_party_planet_inside";
         : if (next_track != current_track) {
            : play_music "music_party_planet_inside";
         }
      }
      : else_if (is_at "cc_win") {
         : set_string var="next_track" text="music_win";
         : if (next_track != current_track) {
            : play_music "music_win";
         }
      }

      : set_string var="current_track" {(next_track)}
   }
   update_volume : subroutine {
      : if (is_sound_muted) {
         : set_music_vol volume="0";
      }
      : else_if (sound_volume == "lowest") {
         : set_music_vol volume="3";
      }
      : else_if (sound_volume == "low") {
         : set_music_vol volume="7";
      }
      : else_if (sound_volume == "normal") {
         : set_music_vol volume="16";
      }
      : else_if (sound_volume == "high") {
         : set_music_vol volume="40";
      }
      : else_if (sound_volume == "highest") {
         : set_music_vol volume="100";
      }
   }
}

######################################
#  On Describe                       #
######################################

on_describe {
   : gosub "play_music";
   : if (is_at "cc_win" ) {
      : print "CONGRATULATIONS!" ;
      : print "YOU FINISHED THE GAME!" ;
      : press_any_key ;
      : clear_screen;
      : stop_music;
      : win_game ;
   }
}

######################################
#  On Command                        #
######################################

on_command {
   : match "follow _;about _; credit _; credits _; readme _; examine cosmoose; cosmoose - ; dhxp -"  {
      : if ((verb_is "about" || verb_is "follow") && is_exists (s1())) {
         : set_sentence {("examine "+ rationalized "noun1")};
      }
      : else {
          : print "YOU CAN FOLLOW OR CHECK COSMOOSE EVERYWHERE: <cosmoose.org>[https://cosmoose.org]." ;
      }
   }
   : match "get all" {
      : if (is_at "cc_room") {
         : print "You are not a baby anymore. No need to take all these toys with you." ;
      }
      : else {
        : print "You don't have proper pockets, that's a little too much.";
      }
   }
   : match "refresh _" {
      : set_sentence "look";
   }
   : match "verb noun" {
      : print "You need to type commands consisting in one verb or one verb and one noun, for example:\n\n>><EXAMINE<#fdd>>\n>><LOOK PIGEON<#fdd>>\n\nType <HELP<#fdd>> to have a summary of useful commands.";
   }
   : match "save cc; help cc" {
      : print "We will do it!";
   }
   : match "examine mirror"  {
      : if (is_at "cc_room") {
         : print "A mirror inspired by antique Venetian aesthetics. Except the usual floral details have been replaced with feathers and bird shapes." ;
      }
   }
   : match "examine star"  {
      : if (is_at "cc_room") {
         : print "You are definitely going to become the number one idol of the Cosmooverse! This star is a gentle reminder." ;
      }
   }
   : match "examine porthole"  {
      : if (is_at "cc_room") {
         : print "A small view on the stars and planets nearby." ;
      }
      : if (is_at "living_room") {
         : print "A premium view on the stars and planets nearby." ;
      }
   }
   : match "examine cube; examine toys"  {
      : if (is_at "cc_room") {
         : print "We all need our toys, no matter how grown up we are." ;
      }
   }
   : match "examine plushie"  {
      : if (is_at "cc_room") {
         : print "An old gift from Wool. Is it a bear, a lion? Who cares, you're too old to play with it." ;
      }
   }
   : match "examine boat"  {
      : if (is_at "cc_room") {
         : print "This boat is an old gift from RuD, a self-proclaimed space pirate who loves Earth pirates too." ;
      }
   }
   : if (is_present "map" && !is_carried "map" && is_at "cc_room") {
      : match "get map" {
        : print "You may be growing up quickly, you still have a lot to learn. This <map<#fee>> may be useful. You take it!";
        : pocket "map";
        : press_any_key;
      }
      : match "exit _" {
         : mask {
            : print "You may be growing up quickly, you still have a lot to learn. This <map<#fee>> may be useful. You take it!";
            : pocket "map";
            : press_any_key;

         }
      }
   }
   : match "examine map; map _; use map" {
      : if (is_carried "map") {
          : set_theme "theme_mono";
          : clear_screen;
          : print {("^n^^c^<______<#000>><________<#000>>|<CC's Bedroom<#" + (is_at "cc_room"?"afa" : "faa")+">>|")};
          : print "^n^^c^--------------------------";
          : print {("^n^^c^|<Wool's Wheels<#" + (is_at "wools_wheels"?"afa" : "faa")+">>| <Common Room<#" + (is_at "living_room"?"afa" : "faa")+">>|")};
          : print "^n^^c^--------------------------";
          : print {("^n^^c^|<Party Planet<#" + (is_at "party_planet"?"afa" : "faa")+">><_<#000>>| <Crispy Yard<#" + (is_at "crispy_garden"?"afa" : "faa")+">>|")};
          : print "^n^^c^--------------------------";
          : print {("^n^^c^⬇<_____________<#000>>")};
          : print {("^n^^c^<Inside<#" + (is_at "party_planet_inside"?"afa" : "aaf")+">><_____________<#000>>")};

          : press_any_key;
          : set_theme "theme_base";
          : redescribe;
      }
      : else_if (is_present "map") {
         : print "A <map<#fee>> of the ship, could it be useful?";
      }
   }
   : match "talk mirror; talk -"   {
      : if (is_at "cc_room") {
         : print "To answer your question: " ;
         : print "Will you become the most famous superstar of the Cosmooverse?" ;
         : press_any_key ;
         : clear_screen;
         : print "No, the most famous superstar of the <Cosmooverse<#fee>> will be a <pigeon<#fee>>. Either musician <OK Feather<#fee>> or bling foodie traveller <E<#fee>>." ;
         : print "That being said, I'm just a talking mirror, whereas you are the true master of your destiny. Follow your dreams or die trying! That's the lesson I learned from <Snow White<#fee>>." ;
         : print "Wow I'm tired after talking so much, I'll take a break. You can replay my previous answer any time. Ciao!" ;
         : press_any_key ;
         : clear_screen;
         : print "You wonder: why pigeons though? You need to investigate and make sure to become the most famous idol of the Cosmooverse! Let's go." ;
         : press_any_key ;
         : redescribe;
      }
   }
   : match "examine cosmooverse"   {
      : print "The universe where you live." ;
   }
   : match "examine ok"   {
      : print "YOU CAN FOLLOW OR CHECK OK FEATHER EVERYWHERE: <okfeather.com>[https://www.okfeather.com]." ;
   }
   : match "examine e"   {
      : print "E is a pigeon influencer. A good friend of June." ;
   }
   : match "examine florrie"  {
       : print "Florrie is an AI programmer, she is currently busy preparing for a <Pygeon<#fee>> programming certification." ;
   }
   : match "examine pygeon"  {
       : print "One of the most popular programming languages, along with Lava, Purrl, Rupee and CC+ (your favorite)." ;
   }
   : match "examine june"   {
      : print "June is your inventor friend. She also makes a lot of music." ;
   }
   : match "examine pigeon; examine dove"   {
      : print "Pigeons are wonderful creatures. If you are not convinced yet, check out how they saved Christmas here: <okfeather.itch.io/feathery-christmas>[https://okfeather.itch.io/feathery-christmas]." ;
   }
   : match "examine snow"   {
      : print "Everyone in the Cosmooverse knows the story of Snow White. The shared destiny of two women wth seemingly nothing in common. One had unlimited beauty and ambition, while the other became a major figure of survivalism after pulling through a severe case of food poisoning." ;
   }
   : match "kill *" {
      : if (is_carried (s1())) {
         : print {("It would be a waste to "+original "verb" + " what I have taken!!!")};
      }
      : else_if (is_present (s1())) {
         : print "The thought could never even start to cross my mind.";
      }
   }
   : match "examine comment" {
      : set_sentence "comment";
   }
   : match "comment _" {
      : set_string var="string_tmp" {(substring {offset -> (strlen {(original "verb")}) string -> (command_text())})};
      : if (string_tmp == "") {
        : if (comments == "") {
            : print "I have not made any <comment<#fdd>> yet";
        }
        : else {
            : print "Here are all the comments I have made:{comments}";
        }
     }
     : else {
        : print "I'm adding that to my list of <comments<#fee>>.";
        : set_string var="comments" {(comments + "\n<_<#000>>" + string_tmp)};
     }

   }
   : match "drop *" {
      : print "You don't need to drop anything.";
   }
   : match "cheat *" {
      : print "Unknown verb - \"cheat\".";
   }

   : match "clear -; redescribe -" {
        : redescribe;
   }

   : match "examine laptop; read laptop; use laptop"  {
      : if (is_at "living_room" && !is_laptop_unlocked) {
         : print "This laptop belongs to <Florrie<#fee>>." ;
         : goto "florrie_laptop";
      }
      : if (is_at "living_room" && is_laptop_unlocked) {
         : print "You swore not to touch Florrie's laptop ever again." ;
      }
      : if (is_at "florrie_laptop") {
        : print "The screen displays:" ;
        : print "Password = answer to this riddle: " ;
        : print "\'My feathers take me anywhere. Everywhere I go there are admirers of my shiny iridescent neck. The tones of grey in my plumage are reminiscent of everyone's favorite weather. " ;
        : print "Who am I? UNLOCK me!\' " ;
      }
   }

   : match "examine tree"  {
      : if (is_at "living_room") {
        : print "How can so many trees grow indoors?" ;
      }
   }
   : match "examine bench; examine table; examine chair "  {
      : if (is_at "living_room") {
        : print "The perfect place to work, chat and drink coffee." ;
      }
   }
   : match "examine screen"  {
      : if (is_at "living_room") {
        : print "This screen sometimes displays important information." ;
      }
      : if (is_at "florrie_laptop") {
        : print "This laptop may be expensive, but you mainly notice the bread and chocolate crumbs stuck inside the keyboard. Florrie is a gourmet." ;
        : print "The screen displays:" ;
        : print "Password = answer to this riddle: " ;
        : print "\'My feathers take me anywhere. Everywhere I go there are admirers of my shiny iridescent neck. The tones of grey in my plumage are reminiscent of everyone's favorite weather. " ;
        : print "Who am I? UNLOCK me!\' " ;
      }
   }

   : match "unlock pigeon; pigeon -"  {
      : if (is_at "florrie_laptop") {
        : print "How can Florrie need clues to remember such an easy password??? " ;
        : print "You wish you had never seen this... Florrie's browser history is nothing but..." ;
        : print "PIGEONS!!!" ;
        : print "Video \'TEQ Talk: Improve Office Efficiency\' shows a pigeon at a desk drinking a protein-packed cheese beverage, and then there's \'Learn Programming in Pighon with E\'... " ;
        : print "How can you compete? So unfair!!! You quickly lock the computer and swear not to touch it ever again." ;
        : press_any_key;
        : set_true "is_laptop_unlocked" ;
        : goto "living_room";
        : redescribe;
      }
    }

    : match "unlock dove"  {
      : if (is_at "florrie_laptop") {
        : print "Almost there, too bad there can only be one password." ;
        : press_any_key;
      }
    }

    : match "unlock - "  {
      : if (is_at "florrie_laptop") {
        : print "Try again." ;
        : press_any_key;
      }
    }

    : match "examine penguin"  {
      : if (is_at "florrie_laptop") {
        : print "This logo was inspired by a scientist contracting penguinitis after a penguin bite. The most notable symptom is unconditional love towards penguins. Similar to pigeonitis." ;
      }
    }

   : match "get laptop"  {
      : if (is_at "living_room" || is_at "florrie_laptop") {
        : print "This is not your <laptop<#fee>>. <Florrie<#fee>> should need it soon." ;
      }
   }

   : match "talk wool; talk -"   {
      : if (is_at "wools_wheels") {
         : print "Why pigeons are so popular?" ;
         : print "I don't know, as I am not interested in physical entities. Only logic moves me." ;
         : press_any_key ;
         : clear_screen;
         : print "Check out this puzzle for instance. I'll give you an easy version: " ;
         : print "An albatross, a bald eagle and a chicken each wear a hat. They see the colors of the others’ hats but not their own. " ;
         : print "Their hats were randomly drawn among five hats, three red ones and two blue ones. " ;
         : print "The albatross is asked to guess its hat color, but responds \'No idea\'. The bald eagle cannot guess its color either." ;
         : print "Then the chicken says \'I know my color\'. Which one is it? Type GUESS color." ;
         : press_any_key ;
      }
   }

   : match "examine wool"   {
      : print "Wool is a mysterious mechanic who can repair anything. Not very talkative, but can be a bit judgemental." ;
   }

   :match "guess red"  {
      : if (is_at "wools_wheels") {
         : print "Correct. The chicken's hat is red of course.";
         : set_true "solved_wool_riddle";
         : print "Alright, maybe answering riddles is one way to become famous? You should try with someone else. Let's go.";
      }
   }

   : match "talk rud; talk -"  {
      : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "RuD greets you warmly: \'CC!!! You look so cute today!!!!\', then begins to whisper:" ;
         : print "\'I'm in great trouble CC. I made a bet with Wool that I'd reach the Diamond Rank in Tweet Fighter before the season ends, but the deadline is today!" ;
         : print "Look: it's supposed to be a fighting game, but now there's this P.U.Z.Z.L.E. thing that's blocking the next level. What's that??? Not fair! Can you HELP me? I only have 10 cans left of Red Gull, but I'll let you take one!\'" ;
      }
   }

   : match "examine rud"  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "RuD looks a bit sadder than usual. A future idol could surely cheer him up!" ;
       }
   }

   : match "examine rud"  {
       : if (is_present "rud_happy" && is_at "party_planet_inside") {
         : print "RuD is always working hard to beat his highest score." ;
       }
   }

   : match "help rud"  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "TODO: puzzle similar to: <layton.fandom.com/wiki/Puzzle:Room_of_Doom>[https://layton.fandom.com/wiki/Puzzle:Room_of_Doom] " ;
         : set_false "is_rud_sad";
         : destroy "rud_sad";
         : create "rud_happy";
         : print "RuD says: \'Nice, next time I'll definitely watch a Let's Play walkthrough before!\'. " ;
         : print "He doesn't look too admirative of your brilliance, as he's already back to playing." ;
         : print "Maybe solving puzzles is not enough for eternal stardom, let's see if there are other options." ;
         : set_true "is_seven_seas_crispy" ;
         : create "seven_seas" target = "crispy_garden" ;
       }
   }

   : match "take road"  {
      : if (is_at "party_planet" && is_exists "rud_sad") {
         : print "You can't right now, it's too early.";
      }
   }

   : match "give laptop"  {
      : if (is_at "living_room") {
         : print "This is not your <laptop<#fee>>." ;
      }
   }

   : match "show compassion"  {
      : if (is_at "party_planet_inside") {
         : print "RuD looks at you with glassy eyes and opens another can of energy drink." ;
      }
   }

   : match "get chicken; taste chicken; grab chicken; seize chicken"  {
      : if (is_at "crispy_garden") {
        : print "You try one of the chicken wings on the tree. It tastes crispy and juicy, it's full of vitamins and grows really fast." ;
      }
   }

   : match "examine chicken; examine tree"  {
      : if (is_at "crispy_garden") {
        : print "Everyone loves chicken trees here. The sweet smell of wings drums and flats evokes summertime." ;
      }
   }

   : match "examine seven"  {
       : if (is_at "crispy_garden") {
         : print "Seven Seas is the walking encyclopedia we all dream of living with. An AI who can analyze any set of data. She <sings<#fee>> during her free time." ;
       }
   }

   : match "talk seven"  {
       : if (is_at "crispy_garden") {
         : print "Seven Seas says: \'Pigeons will dominate online stardom? That does not coincide with the data I have!" ;
         : print "To become a successful online entertainer, one must not only upload regular content with professional editing, but also find a unique style and niche audience! It's a lot of work!\'" ;
         : print "Sounds really dull. Let's ask Seven Seas who's the most famous star right now, it will very likely contradict what she just said." ;
         : print "\'Hey CC, you look bored. Why don't you try to find out by yourself? \'" ;
         : press_any_key ;
         : clear_screen;
         : print "\'The four current most popular online hosts are:" ;
         : print "\'beats to mow the lawn to\', a music DJ" ;
         : print "\'C-Cherries\', introducing movie trailers" ;
         : print "\'DewDieDie\', a video game expert" ;
         : print "\'8k-Minute Recipes\', a cooking enthusiast." ;
         : print "3 of them are equally popular, but there is only 1 number one!" ;
         : print "You can compare them pairwise. What is the minimal number of pair comparisons you need to determine who is number one? GUESS the number!\'" ;
         }
   }

   : match "guess two; guess 2"  {
       : if (is_at "crispy_garden") {
         : print "Indeed, you need 2 comparisons at least. In this case, 8k-Minute Recipes and DewDieDie have the same success. But DewDieDie has been beaten by C-Cherries, who is therefore the most successful star." ;
         : print "C-Cherries looks very wealthy. That's probably the main explanation for this incredible success." ;
         : set_true "can_leave_crispy_garden" ;
         : set_true "can_create_june_e" ;
         : create "june" target = "living_room";
         : create "ee" target = "living_room";
         : set_true "is_june_in_living_room" ;
         : set_true "is_e_in_living_room" ;
         : press_any_key ;
         : redescribe ;
       }
   }

   : match "sing _" {
      : if (is_present "seven_seas") {
        : print "Seven Seas starts to sing: \'I have a dream, about a place I wanna go...\' but suddenly takes a louder deeper voice and frenetically repeats \'DAPHNE DAPHNE DAPHNE...\'." ;
      }
      : else_if (is_present "rud_sad") {
        : print "RuD sings Cosmoose's hit \'Release Me\': \'If we'll make it outta here, I'll need a vacation \'" ;
      }
      : else_if (is_present "rud_happy") {
        : print "RuD sings Cosmoose's hit \'I wanna conquer youuuuu yeah!\'. Who knew he had such golden pipes?" ;
      }
      : else_if (is_present "wool") {
        : print "Wool sings \'Time to clap your hands, clap your hands and GO!\'. Who knew he liked such mainstream music?" ;
      }
      : else_if (is_present "june" && is_present "ee") {
        : print "June and E sing OK Feather's hit \'Coocoo Cafe\'. You can listen to this song at the end of the game <Feathery Christmas>[https://okfeather.itch.io/feathery-christmas]." ;
      }
      : else {
         : print "You start singing your favourite viral song, \"Cosmoose Gang\".";
      }
   }

   : match "x fresh; x air; x smell; get fresh; get air; x smell; smell _"  {
      : if (is_at "cc_room") {
         : print "Let's <exit<#fdd>> and get some fresh air.";
      }
      : else_if (is_at "party_planet_inside") {
         : print "The mixed smell of spilled energy drinks and crisps really makes me want to cook a proper meal.";
      }
      : else {
         : print "It's nice to get some fresh air.";
      }
   }

   : match "talk june"  {
      : set_false "is_seven_seas_crispy" ;
      : destroy "seven_seas" ;
      : print "TODO June says: \'Wanna try my Pigeoshop software? You can then transform any face or body into a realistic pigeon!" ;
      : print "Florrie uses it all the time. Well, she has no choice because I secretly hacked her laptop, but her password is so easy to remember! \'" ;
      : print "OK... That explains why Florrie's browsing history was only about pigeons. So the mirror was lying?" ;
      : press_any_key ;
      : clear_screen;
      : print "\'Does the mirror I gave you always tell the whole truth? Well no machine can tell the whole truth! " ;
      : print "Otherwise the world would be forever at peace, don't you think? However, what I know for sure is MY TRUTH!" ;
      : print "In my eyes, pigeons will always be my number one idols.\'" ;
      : print "Wow, why does it feel like today was a very wasteful day?" ;
      : redescribe ;
   }

   : match "talk e"  {
      : set_false "is_seven_seas_crispy" ;
      : destroy "seven_seas" ;
      : print "TODO \'The secret of my viral videos?" ;
      : print "Being completely invested and passionate about what I do. My patrons send me coupons to try new pizzas, and I document my experience spending these coupons. " ;
      : print "Sometimes I receive 50 coupons the same day, but I am always committed to using every fresh daily coupon. I think my dedication is what brings happiness to my fans." ;
      : print "One fan gifted me this gold necklace with my name, I will always cherish it." ;
      : print "And you know it's a real job, you don't just do this for yourself. One subscriber called RuDenergy paid a hefty amount to see a comparison review of different energy drinks." ;
      : print "How to describe these beverages... My disappointment was immeasurable and my day was ruined. But sometimes your fans are more important, and it was good money.\'" ;
      : print "TODO: finish the game" ;
   }

   : match "examine yourself; talk yourself"  {
      : if (is_hero_cc) {
        : print "You are so cute, whether it's as CC or HeroCC." ;
      }
      : else {
        : print "You are so cute. If only you could be everywhere at once so the world could see your cuteness. This is why your goal is to become a superstar." ;
      }
   }

   : match "cheat win" {
      # TODO
      : print "TODO" ;
      : goto "cc_win" ;
   }

   : match "help -" {
       : print "- Type '<I<#fdd>>' to see what you are holding";
       : print "- Type '<GET<#fdd>> <objectname<#fee>>' to pick up an item";
       : print "- Type '<GIVE<#fdd>> <objectname<#fee>>' to give an object to someone";
       : print "- Type '<X<#fdd>>' or '<X<#fdd>> <objectname<#fee>>' to examine clues";
       : print "- Type <N<#fdd>>, <S<#fdd>>, <E<#fdd>>, <W<#fdd>>, <UP<#fdd>>, <DOWN<#fdd>>, <ENTER<#fdd>>, <EXIT<#fdd>> to move";
       : print "- Type '<LOAD<#fdd>>' or  '<SAVE<#fdd>>' to load or save a game";
       : print "- Type '<QUIT<#fdd>>' to restart a game";
   }

   : match "examine -"  {
      : if (is_at "cc_room") {
         : print "This room is so cute, just like me." ;
      }
      : if (is_at "crispy_garden") {
         : print "It's quite warm in here." ;
      }
      : if (is_at "florrie_laptop") {
         : print "Florrie's laptop screen shows the <Cosmoose<#fee>> logo and a mysterious <penguin<#fee>>." ;
      }
      : if (is_at "living_room") {
         : print "This room has everything, from nature to technology, and what a nice view on the neighboring planets." ;
      }
      : if (is_at "party_planet") {
         : print "It may look gloomy from the outside, but some people spend their nights and day inside." ;
      }
      : if (is_at "party_planet_inside") {
         : print "RuD is almost always here. He moved all the stuff from his own bedroom (which looks like an empty garage now) to this common gaming room." ;
      }
      : if (is_at "wools_wheels") {
         : print "Wool will repair any gear you bring, mostly with the power of his brain." ;
      }
   }
}

######################################
#  Assets                     #
######################################

assets {
   sounds {
      music_beat_1 : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/Beat_1.mp3" ;
      music_beat_2 : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/Beat_2_ccs_room.mp3" ;
      music_wools_wheels : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/lofi2_wool.mp3" ;
      music_living_room : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/lofi3_livingroom.mp3" ;
      music_party_planet_inside : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/lofi4_partyplanet.mp3" ;
      music_party_planet_outside: sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/lofi8_party_planet_outside.mp3" ;
      music_crispy_garden : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/lofi10_crispy.mp3" ;
      music_win : sound_sample "https://cosmoosic.github.io/adventuron_april_2022/Music/funprio.mp3" ;
   }
   graphics {
      cc_room : png "https://cosmoosic.github.io/adventuron_april_2022/Images/cc_room.png" ;
      florrie_laptop : png "https://cosmoosic.github.io/adventuron_april_2022/Images/laptop.png" ;
      crispy_garden : png "https://cosmoosic.github.io/adventuron_april_2022/Images/crispy_garden_no7Cs.png" {
        : if (is_seven_seas_crispy) {
               : overlay "crispy_garden_7Cs";
        }
      } ;
      wools_wheels : png "https://cosmoosic.github.io/adventuron_april_2022/Images/wools_wheels.png" {
        : if (is_wool_wheeled) {
               : overlay "wool";
        }
      } ;
      party_planet : png "https://cosmoosic.github.io/adventuron_april_2022/Images/party_planet.png" ;
      party_planet_inside : png "https://cosmoosic.github.io/adventuron_april_2022/Images/rud.png" {
        : if (!is_rud_sad) {
               : overlay "rudface";
        }
      } ;
      rudface : png "https://cosmoosic.github.io/adventuron_april_2022/Images/rudface.png" ;
      crispy_garden_7Cs : png "https://cosmoosic.github.io/adventuron_april_2022/Images/crispy_garden_7Cs.png" ;
      wool : png "https://cosmoosic.github.io/adventuron_april_2022/Images/wool.png" ;
      june : png "https://cosmoosic.github.io/adventuron_april_2022/Images/june.png" ;
      etana : png "https://cosmoosic.github.io/adventuron_april_2022/Images/etana.png" ;
      florrie : png "https://cosmoosic.github.io/adventuron_april_2022/Images/florrie.png" ;
      florrie_cake : png "https://cosmoosic.github.io/adventuron_april_2022/Images/florrie_cake.png" ;
      living_room : png "https://cosmoosic.github.io/adventuron_april_2022/Images/common_room.png" {
        : if (is_june_in_living_room) {
               : overlay "june";
               : if (is_e_in_living_room) {
                 : overlay "etana";
               }
        }
        : else_if (is_e_in_living_room) {
               : overlay "etana";
        }
        : else_if (is_florrie_in_living_room) {
               : overlay "florrie";
        }
        : else_if (is_florrie_in_living_room_with_cake) {
               : overlay "florrie_cake";
        }
        : else_if (is_everybody_here) {
               : overlay "everyone";
        }


      } ;
      everyone : png "https://cosmoosic.github.io/adventuron_april_2022/Images/everyone.png" ;
      loading_screen : png "https://cosmoosic.github.io/adventuron_april_2022/Images/loading.png" ;
      cc_hero_june : png "https://cosmoosic.github.io/adventuron_april_2022/Images/cc_hero.png" ;
      cc_win : png "https://cosmoosic.github.io/adventuron_april_2022/Images/feast.png" ;
   }
}
