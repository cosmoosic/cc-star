######################################
#  Adventuron                        #
######################################

game_information {
   game_name                    = CC's Road to Stardom
   game_version                 = 1.0.0
   game_shortname               = CC Star
   written_by                   = OK Feather
   uuid                         = 910e5474-beef-11ec-9d64-0242ac120002
   year_of_release              = 2022
   short_synopsis               = Help 50% cat, 50% bunny, 100% cute CC become an online idol!
}

themes {
  theme_base : theme {
      colors {
         exit_list_item_pen = #fdd
      }
      lister_exits {
         is_list_enter = true
      }
  }
  theme_mono : theme {
      theme_settings {
         font = daad
      }
   }
}

######################################
#  Game Settings                     #
######################################

game_settings {
   auto_ask_sound_permissions = false
   enable_standard_all_behaviour = false
}


loading_screen = loading_screen
redescribe = auto_beta

start_at = cc_room

vocabulary {
   : verb / aliases = [examine, check, regarde, touch, look, see]
   : verb / aliases = [give, show]
   : verb / aliases = [up, upstair, upstairs]
   : verb / aliases = [down, downstair, downstairs]
   : verb / aliases = [talk, say, speak, tell, ask, wave, discuss, chat, t]
   : verb / aliases = [get, take, pocket, catch, pick]
   : verb / aliases = [comment, comments, note, notes]
   : verb / aliases = [exchange, exchanging]
   : verb / aliases = [exit, leave, outside, out, flee, escape, no, refuse]
   : verb / aliases = [enter, inside, in]
   : verb / aliases = [use, drink, eat, consume, swallow]
   : verb / aliases = [throw, discard, vomit, puke]
   : verb / aliases = [ring,strike]
   : verb / aliases = [move, go]
   : verb / aliases = [guess, reply]
   : verb / aliases = [thank, thanks]
   : verb / aliases = [happy, merry, feliz, joyous, feathery, great, joyeux, enjoy, warmest, jolly, cheerful, 행복한, 메리, meli]
   : verb / aliases = [kill,threaten,menace,intimidate,browbeat,bully,scare,frighten,terrorize,murder,assassinate,beat,fight,combat,hurt,kick,harm,injure,insult,destroy,smash]
   : verb / aliases = ["cry", ":(","=(", "='(", ":'("]
   : verb / aliases = ["smile", "grin", "laugh", ":)","=)",":D","=D"]
   : verb / aliases = [black, yellow, pink, white, colorless, rainbow, purple, green]
   : verb / aliases = [unlock, password, pass, type, write, input]
   : noun      / aliases = [bread, croissant, pain, cookie, loaf, biscuit]
   : noun      / aliases = [cafe, coocoo, shop, restaurant]
   : noun      / aliases = [owner, vendor]
   : noun      / aliases = [pigeon]
   : noun      / aliases = [child, children, kid]
   : noun      / aliases = [comment, note]
   : noun      / aliases = [bird, dove, birb]
   : noun      / aliases = [door, entrance]
   : noun      / aliases = [energy, drink, beverage, redgull, can, red, potion, barrel, taurine, juice, clayrox, clorox]
   : noun      / aliases = [mirror, reflection]
   : noun      / aliases = [garden, park, yard, lawn, land, plot, greenhouse, orchard]
   : noun      / aliases = [fight, combat, conflict]
   : noun      / aliases = [floor, ground, flooring]
   : noun      / aliases = [up, upstair, upstairs]
   : noun      / aliases = [laptop, computer]
   : noun      / aliases = [screen, display]
   : noun      / aliases = [porthole, window, view, sky, scenery]
   : noun      / aliases = [snow, white, snowwhite]
   : noun      / aliases = [chicken, fruit, wing]
   : noun      / aliases = [banach, banachspherificator, spherificator, sphere]
   : noun      / aliases = [bull, cow, bouncy, bouncybull, bluecow]
   : noun      / aliases = [plush, plushie, stuffedanimal, animal, stuffed, lion, bear, teddy, teddybear, stuffie, plushtoy, stuffy, plushy]
   : noun      / aliases = [boat, ship, barge, gondola, yacht, sailboat, ark, bateau]
   : noun      / aliases = [python, pighon, pygeon, pyghon]
   : noun      / aliases = [puzzle, enigma, riddle, mystery, problem]
   : noun      / aliases = [guitar, music, bass, instrument]
   : noun      / aliases = [tool, machine, gadget, appliance, kirby, device, toolbox, furniture, gear, pyjeon]
   : noun      / aliases = [tree, pine, forest, plant]
   : noun      / aliases = [room, chamber, place, here]
   : noun      / aliases = [joystick, trackball, gamepad, joypad, trackpad, stick, controller, arcade]
   : noun      / aliases = [blue, black, yellow, pink, white, colorless, rainbow, purple, green]
   : noun      / aliases = [seven, 7, 7C, 7Cs, sevenseas, robot, bot]
   : noun      / aliases = [necklace, chain, jewel, euro, gold]
   : noun      / aliases = [rud, ru, duri, dhxp, gamer, r]
   : noun      / aliases = [wool, woolion, mechanic, lion, w]
   : noun      / aliases = [june, inventor, jjyy, j]
   : noun      / aliases = [florrie, flora, f]
   : noun      / aliases = [e, ee, etana]
   : noun      / aliases = [yourself, cc, myself, me, you]
}

######################################
#  Variables                         #
######################################

booleans {
   can_leave_crispy_garden : boolean "false";
   help_about_commands : boolean "false";
   is_sound_muted : boolean "true";
   music_ever_enabled : boolean "false";
   is_puzzle_wool_solved : boolean "false" ;
   can_create_june_e : boolean "false";
   is_seven_seas_crispy : boolean "true";
   is_wool_wheeled : boolean "true";
   is_laptop_unlocked : boolean "false";
   is_rud_sad : boolean "true" ;
   is_hero_cc : boolean "false" ;
   is_everybody_here : boolean "false" ;
   is_june_in_living_room : boolean "false" ;
   is_e_in_living_room : boolean "false" ;
   is_florrie_alone_in_living_room : boolean "false" ;
   is_florrie_in_living_room_with_cake : boolean "false" ;
   is_puzzle_rud_in_progress : boolean "false" ;
   is_puzzle_seven_seas_now : boolean "false" ;
   is_puzzle_ee_solved : boolean "false" ;
   is_puzzle_florrie_now : boolean "false" ;
   is_puzzle_florrie_solved : boolean "false" ;
   has_florrie_drank : boolean "false" ;
   you_win : boolean "false" ;
   talked_to_june : boolean "false" ;
   talked_to_e : boolean "false" ;
   talked_to_rud : boolean "false" ;
   talked_to_mirror_and_solved_puzzles : boolean "false" ;
   rud_sad_talked : dynamic_boolean {(
      talked_to_rud &&
      is_rud_sad
   )}
   has_blueprint : boolean "false";
   in_labyrinth : boolean "false";
   is_labyrinth_open : boolean "false";
   is_playing_logic  : boolean "false";
   talked_to_someone_before_cake : boolean "false";
}

integers {
   pitch_random : integer "0";
   inventory_limit : integer "30" ;
   day_spend_labyrinth_enigma : integer "0";
   labyrinth_position_x : integer "0";
   labyrinth_position_y : integer "-13";
   choking_e_solution : integer "0";
   choking_e_form : integer "0";
   integer_tmp : integer "0";
}

collections {
   labyrinth_dir : list {
      items  = [ "1_1", "1_2", "1_3", "1_4", "1_5", "2_1", "2_2", "2_3", "2_4", "2_5", "3_1", "3_2", "3_3", "3_4", "3_5", "4_1", "4_2", "4_3", "4_4", "4_5", "5_1", "5_2", "5_3", "5_4", "5_5" ]
   }

   choking_e_choice_1: list {
    items = [
        "Give 5 abdominal thrusts (also known as the Heimlich maneuver).",
        "Alternate between 5 blows and 5 thrusts until the blockage is dislodged.",
        "Give 5 back blows.",
    ]
   }

   choking_e_choices: list {
      items = [choking_e_choice_1, choking_e_choice_1, choking_e_choice_1]
   }

   choking_e_response_1: list {
        items = [
            "{rescue_person_1} begins to give 5 back blows, you stay behind and observe. {rescue_person_3} also helps.",
            "{rescue_person_2} begins to give 5 back blows, you stay behind and observe. {rescue_person_4} also helps.",
            "Yes, you are awesome!!!! You give 5 back blows, and E looks a bit better. {rescue_person_5} also helps.",
        ]
   }

   choking_e_response_2: list {
        items = [
            "Yes, you are awesome!!!! You do the Heimlich maneuver, and E seems a bit more alive.",
            "{rescue_person_3} begins to do the Heimlich maneuver, you stay behind and observe.",
            "{rescue_person_4} begins to do the Heimlich maneuver, you stay behind and observe.",
        ]
   }

   choking_e_response_3: list {
        items = [
            "{rescue_person_5} begins to alternate between 5 blows and 5 thrusts, you stay behind and observe.",
            "Yes, you are awesome!!!! You alternate between 5 blows and 5 thrusts, and it really looks like the last step to save E!",
            "{rescue_person_6} begins to alternate between 5 blows and 5 thrusts, you stay behind and observe.",
        ]
   }

   choking_e_responses: list {
      items = [choking_e_response_1, choking_e_response_2, choking_e_response_3]
   }

   choking_e_introductions: list {
      items = [
         "We need to save E!"
      ]
   }

   choking_e_queries: list {
      items = [
         "We need to save E! What is the first step to help him?",
         "It's not over yet! What is the second step to help him?",
         "Almost there! What is the third step to help him?",
      ]
   }

   rescue_people: set {items = [June, SevenSeas, Wool, RuD, Kirby, Pyjeon]}

   choking_e_solutions: list {
        items = [2, 0, 1]
   }
   choking_e_order: list {
      items = [0, 1, 2]
   }
}

strings {
   next_track : string "";
   current_track : string "";
   sound_volume : string "normal";
   string_tmp : string "";
   comments : string "";
   nothing_message : string "";
   string_correction : string "";
   rescue_person_1 : string "";
   rescue_person_2 : string "";
   rescue_person_3 : string "";
   rescue_person_4 : string "";
   rescue_person_5 : string "";
   rescue_person_6 : string "";
}

######################################
#  Locations                         #
######################################

locations {

   cc_room : location "Hello CC! This is your bedroom, typical of rooms in spaceships nowadays. Today is your birthday, so your inventor friend <June<#cc0>> made a custom magic <mirror<#cc0>> that answers your questions. Feel free to <TALK<#cc0>> to the mirror." ;

   cc_hero_june : location "June helped you transform into HeroCC thanks to her magic <beak<#cc0>>." ;

   cc_win : location "Everybody is here, finally we can eat cake!" ;

   crispy_garden : location "You are in the Crispy Garden. You see trees where <chicken wings<#cc0>> grow.";

   living_room : location "You are in the common living room." ;

   party_planet : location "You are in front of Party Game Planet, the gaming room. Maybe you should ENTER?" ;

   party_planet_inside : location "You are inside the gaming room." ;

   wools_wheels : location "You are at Wool's Wheels, <Wool<#cc0>>'s vehicle and ship repair area." ;

   puzzle_florrie_laptop : location "You are in front of Florrie's laptop. Can you <UNLOCK<#cc0>> or <TYPE<#cc0>> her password?";

   puzzle_hat : location "Wool says: \'I can show you my favorite puzzle of when I was your age. <TALK<#cc0>> to me whenever you are ready.\'";

   puzzle_labyrinth : location "The only path trough the asteroid belt is filled with drones! Their sensors only see in front of their purple cannon. But there's one we can hack to get the blueprint. We can turn this situation around!";

   labyrinth_1_1: location "Be careful of their purple laser cannons, and get the blueprint!";
   labyrinth_1_2: location "";
   labyrinth_1_3: location "It was a trap, there's no way to continue!";
   labyrinth_1_4: location "";
   labyrinth_1_5: location "How did you get there?";
   labyrinth_2_1: location "";
   labyrinth_2_2: location "";
   labyrinth_2_3: location "You won't be able to get the blueprint...";
   labyrinth_2_4: location "";
   labyrinth_2_5: location "Almost there...";
   labyrinth_3_1: location "It was a trap, there's no way to continue!";
   labyrinth_3_2: location "";
   labyrinth_3_3: location "";
   labyrinth_3_4: location "";
   labyrinth_3_5: location "Closer...";
   labyrinth_4_1: location "";
   labyrinth_4_2: location "Hack successful! You got the blueprint!";
   labyrinth_4_3: location "The hacking tool is ready, just go in front of the blue sensor!";
   labyrinth_4_4: location "";
   labyrinth_4_5: location "";
   labyrinth_5_1: location "We need to exit this mine field...";
   labyrinth_5_2: location "";
   labyrinth_5_3: location "";
   labyrinth_5_4: location "We're so close, yet so far!";
   labyrinth_5_5: location "You made it! Congratulations!";

}

######################################
#  Connections                       #
######################################

connections {

   from, direction, to = [
   cc_room, south, living_room,
   living_room, west, wools_wheels,
   wools_wheels, south, party_planet,
   party_planet, enter, party_planet_inside,
   party_planet, east, crispy_garden,
   crispy_garden, north, living_room,

   living_room, enter, puzzle_labyrinth,
   labyrinth_1_1, east_oneway, labyrinth_1_2,
   labyrinth_1_1, south_oneway, labyrinth_2_1,
   labyrinth_1_2, east_oneway, labyrinth_1_3,
   labyrinth_1_2, south_oneway, labyrinth_2_2,
   labyrinth_1_3, east_oneway, living_room,
   labyrinth_1_3, south_oneway, living_room,
   labyrinth_1_4, east_oneway, living_room,
   labyrinth_1_5, south_oneway, labyrinth_2_5,
   labyrinth_2_1, east_oneway, living_room,
   labyrinth_2_1, south_oneway, labyrinth_3_1,
   labyrinth_2_2, south_oneway, labyrinth_3_2,
   labyrinth_2_3, west_oneway, living_room,
   labyrinth_2_3, east_oneway, labyrinth_2_4,
   labyrinth_2_4, east_oneway, labyrinth_2_5,
   labyrinth_2_5, south_oneway, labyrinth_3_5,
   labyrinth_3_1, south_oneway, living_room,
   labyrinth_3_1, east_oneway, living_room,
   labyrinth_3_2, east_oneway, labyrinth_3_3,
   labyrinth_3_3, south_oneway, labyrinth_4_3,
   labyrinth_3_3, north_oneway, labyrinth_2_3,
   labyrinth_3_4, west_oneway, living_room,
   labyrinth_3_4, north_oneway, labyrinth_2_4,
   labyrinth_3_5, west_oneway, living_room,
   labyrinth_3_5, south_oneway, labyrinth_4_5,
   labyrinth_4_1, south_oneway, labyrinth_5_1,
   labyrinth_4_2, west_oneway, labyrinth_4_1,
   labyrinth_4_2, south_oneway, living_room,
   labyrinth_4_2, north_oneway, labyrinth_3_2,
   labyrinth_4_3, west_oneway, labyrinth_4_2,
   labyrinth_4_3, south_oneway, living_room,
   labyrinth_4_4, north_oneway, labyrinth_3_4,
   labyrinth_4_4, east_oneway, living_room,
   labyrinth_4_4, west_oneway, living_room,
   labyrinth_4_5, south_oneway, labyrinth_5_5,
   labyrinth_5_1, east_oneway, labyrinth_5_2,
   labyrinth_5_2, east_oneway, labyrinth_5_3,
   labyrinth_5_3, east_oneway, labyrinth_5_4,
   labyrinth_5_4, east_oneway, living_room,
   labyrinth_5_4, north_oneway, labyrinth_4_4,
   ]

}

######################################
#  Objects                           #
######################################

objects {

chicken_tree : object "a typical <chicken tree<#cc0>>" at = "crispy_garden" ;

ee : scenery "<pigeon E<#cc0>>, a friend of June who made a few viral videos" ;

florrie : scenery "programmer <Florrie<#cc0>>" ;

june : scenery "inventor <June<#cc0>>, who made you the custom <mirror<#cc0>>" ;

laptop : scenery "a <laptop<#cc0>> computer" at = "living_room";

screen : scenery "a large digital <screen<#cc0>>" at = "living_room";

map : object "a <map<#cc0>> of the ship" at = "cc_room";

energy_drink : object "a brand new can of energy <drink<#cc0>>";

chicken_wing : object "a ripe chicken wing" at = "crispy_garden";

spherificator : object "a Banach-Spherificator" at = "wools_wheels";

mirror : object "the <mirror<#cc0>> June made for you" at = "cc_room";

rud_happy : scenery "<RuD<#cc0>>, who looks way more energized now";

rud_sad : scenery "<RuD<#cc0>>, who is almost always there" at = "party_planet_inside";

seven_seas : scenery "one of the most data savvy robots, called <SevenSeas<#cc0>> or <7Cs<#cc0>>" ;

wool : scenery "mechanic <Wool<#cc0>>, who does all kinds of repair work here" at = "wools_wheels";

wool_puzzled : scenery "mechanic <Wool<#cc0>>, showing his favorite puzzle";

wool_kitchen : scenery "<Wool<#cc0>>" ;
sevenseas_kitchen : scenery "<SevenSeas<#cc0>>" ;
rud_kitchen : scenery "<RuD<#cc0>>" ;
june_kitchen : scenery "<June<#cc0>>" ;
ee_kitchen : scenery "<E<#cc0>>" ;

}

######################################
#  Barriers                          #
######################################

barriers {
   block_laptop_wools_wheels : block_path {
      from = living_room
      to = wools_wheels
      message  = Curiosity makes your mind more active and open to new ideas. That's why health professionals would recommend unlocking this computer for your personal growth.
      block_when_not = is_laptop_unlocked

   }

   block_wool : block_path {
      from = wools_wheels
      to = party_planet
      message  = You should talk to Wool first, he spends way too much time alone, that's not healthy at all.
      block_when_not = is_puzzle_wool_solved
      show_blocked_exit = true

   }

   block_leave_party_planet : block_path {
      from = party_planet_inside
      to = party_planet
      message  = Please assist RuD and show some compassion, he has to finish his game and cannot lose face.
      block_when = rud_sad_talked
      show_blocked_exit = true

   }

   block_rud_sad : block_path {
      from = party_planet
      to = crispy_garden
      message  = The yard is still chilly, let's stay here for a bit.
      block_when = is_rud_sad
      show_blocked_exit = true

   }

   block_seven_seas : block_path {
      from = crispy_garden
      to = living_room
      message  = You should enjoy the view a little longer.
      block_when_not = can_leave_crispy_garden
      show_blocked_exit = true
   }

   block_living_room_to_crispy_garden : block_path {
      from = living_room
      to = crispy_garden
      message  = The yard is too chilly, let's stay here for a bit.
      block_when = is_rud_sad
      show_blocked_exit = true
   }

   block_labyrinth : block_path {
      from = living_room
      to = puzzle_labyrinth
      message  = There is nothing interesting on the screen. Navigation data, space information... how boring.
      block_when_not = is_labyrinth_open
      show_blocked_exit = true
   }
}

######################################
#  On Startup                        #
######################################

on_startup {
   : iterate "labyrinth_dir" {
       : set_graphic {
         graphic -> ("puzzle_labyrinth")
         target -> ("labyrinth_" + item())
       }
   }

   : if (!sysvar_bool "sysvar_has_asked_sound") {
      : print "CC's Road to Stardom uses background music.\n\nYou can lower the volume or stop the music at any moment using <SOUND LOWEST<#cc0>> or <SOUND OFF<#cc0>>.\n\nDo you wish to enable the music?\n";
      : gosub "set_sound_on";
      : if (!sysvar_bool "sysvar_music_enabled") {
         : print "You can enable the music at any moment with <SOUND ON<#cc0>>.";
         : press_any_key;
      }
      : clear_screen;
   }
   : else_if (sysvar_bool "sysvar_music_enabled") {
      : set_true "music_ever_enabled";
      : if (sysvar_bool "sysvar_sound_enabled") {
         : set_false "is_sound_muted";
         : print "You can lower or stop the sound at any moment with <SOUND LOWEST<#cc0>> or <SOUND OFF<#cc0>>.\n";
         : press_any_key;
      }
      : else {
         : print "CC's Road to Stardom uses background music. You can enable the music at any moment with <SOUND ON<#cc0>> or <SOUND LOWEST<#cc0>> for lower volume.\n";
         : press_any_key;
      }
   }
   : gosub "update_volume";
   : play_music "music_beat_2";
   : print "Type <VERB NOUN<#cc0>> with the keyboard to advance in the game." ;
   : if (is_mobile()) {
      : print "You can touch some coloured words to copy them, or touch and hold to choose an action.";
   }
   : else {
      : print "You can click some coloured words to copy them, or right-click to choose an action.";
   }
   : print "e.g. <EXAMINE PIGEON<#cc0>> (or <X<#cc0>>) to take a closer look at a PIGEON." ;
   : print "<GET CHICKEN<#cc0>> to take CHICKEN and add it to your inventory." ;
   : press_any_key ;
   : print "To travel, type <NORTH<#cc0>>, <EAST<#cc0>>, <SOUTH<#cc0>>, <WEST<#cc0>> (or <N<#cc0>>, <E<#cc0>>, <S<#cc0>>, <W<#cc0>>), <ENTER<#cc0>>, <EXIT<#cc0>>, <UP<#cc0>>, <DOWN<#cc0>>." ;
   : press_any_key ;
   : print "To check your inventory, type <INVENTORY<#cc0>> (or <I<#cc0>>)." ;
   : press_any_key ;
   : print "To see available commands at a location, type <HELP<#cc0>>." ;
   : press_any_key ;

}

######################################
#  Subroutines                       #
######################################

subroutines {
   set_sound_on : subroutine {
      : if (!sysvar_bool "sysvar_sound_enabled") {
         : ask_permission ask_again_and_again = "true" category = "sound" ;
      }
      : if (sysvar_bool "sysvar_sound_enabled" && !sysvar_bool "sysvar_music_enabled") {
            : ask_permission ask_again_and_again = "true" category = "music" ;
      }
      : if (sysvar_bool "sysvar_music_enabled") {
         : set_false "is_sound_muted";
         : gosub "update_volume";
         : set_true "music_ever_enabled";
      }
   }
   random_music : subroutine {
      : if (is_sound_muted) {
         : return;
      }
      : store_random var = "pitch_random";
      : if (pitch_random < 20) {
        :  beep millis="200" pitch="30";
      }
      : else_if (pitch_random < 40) {
        :  beep millis="200" pitch="33";
      }
      : else_if (pitch_random < 60) {
        :  beep millis="200" pitch="36";
      }
      : else_if (pitch_random < 80) {
        :  beep millis="200" pitch="39";
      }
      : else {
        :  beep millis="200" pitch="42";
      }
   }
   play_music : subroutine {
      : if (is_sound_muted) {
         : return;
      }
      : if (is_at "cc_room") {
         : set_string var="next_track" value="music_beat_2";
         : if (next_track != current_track) {
            : play_music "music_beat_2";
         }
      }
      : else_if (is_at "party_planet") {
         : set_string var="next_track" value="music_party_planet_outside";
         : if (next_track != current_track) {
            : play_music "music_party_planet_outside";
         }
      }
      : else_if (is_at "puzzle_florrie_laptop") {
         : set_string var="next_track" value="music_short";
         : if (next_track != current_track) {
            : play_music "music_laptop";
         }
      }
      : else_if (is_at "wools_wheels") {
         : set_string var="next_track" value="music_wools_wheels";
         : if (next_track != current_track) {
            : play_music "music_wools_wheels";
         }
      }
      : else_if (is_at "crispy_garden") {
         : set_string var="next_track" value="music_crispy_garden";
         : if (next_track != current_track) {
            : play_music "music_crispy_garden";
         }
      }
      : else_if (is_at "living_room" && (is_present "june" || is_present "ee")) {
         : set_string var="next_track" value="music_living_room_pigeon";
         : if (next_track != current_track) {
            : play_music "music_living_room_pigeon";
         }
      }
      : else_if (is_at "living_room" && (is_everybody_here || is_florrie_in_living_room_with_cake)) {
         : set_string var="next_track" value="music_win";
         : if (next_track != current_track) {
            : play_music "music_win";
         }
      }
      : else_if (is_at "living_room") {
         : set_string var="next_track" value="music_living_room";
         : if (next_track != current_track) {
            : play_music "music_living_room";
         }
      }
      : else_if (is_at "party_planet_inside") {
         : set_string var="next_track" value="music_party_planet_inside";
         : if (next_track != current_track) {
            : play_music "music_party_planet_inside";
         }
      }
      : else_if (in_labyrinth || is_at "puzzle_labyrinth") {
         : set_string var="next_track" value="music_labyrinth";
         : if (next_track != current_track) {
            : if (chance(33)) {
               : play_music "music_labyrinth_1";
            }
            : else_if (chance(34)) {
               : play_music "music_labyrinth_2";
            }
            : else {
                : play_music "music_beat_1";
            }
         }
      }
      : else_if (is_at "cc_win") {
         : set_string var="next_track" value="music_win";
         : if (next_track != current_track) {
            : play_music "music_win";
         }
      }

      : set_string var="current_track" {(next_track)}
   }
   update_volume : subroutine {
      : if (is_sound_muted) {
         : set_music_vol volume="0";
      }
      : else_if (sound_volume == "lowest") {
         : set_music_vol volume="3";
      }
      : else_if (sound_volume == "low") {
         : set_music_vol volume="7";
      }
      : else_if (sound_volume == "normal") {
         : set_music_vol volume="16";
      }
      : else_if (sound_volume == "high") {
         : set_music_vol volume="40";
      }
      : else_if (sound_volume == "highest") {
         : set_music_vol volume="100";
      }
   }
   play_choking_e_puzzle_choice : subroutine {
      : set_integer var="integer_tmp" {(
         int(collection_get { collection="choking_e_order" index -> (integer_tmp)})
      )}
      : set_string var="string_tmp" {(
         collection_get { collection -> (collection_get { collection="choking_e_responses" index -> (choking_e_form)}) index -> (integer_tmp)}
      )}
      : print "{string_tmp}";
      : if (choking_e_solution == integer_tmp) {
         : increment "choking_e_form";
         : if ((count "choking_e_responses") == choking_e_form) {
            : set_true "is_puzzle_ee_solved";
            : goto "living_room" ;
         }
         : else {
            : gosub "play_choking_e_puzzle" ;
         }
      }
      : else {
         : gosub "play_choking_e_puzzle" ;
      }
   }
   play_choking_e_puzzle_next_response_line :  subroutine {
      : set_integer var="integer_tmp" {(
         int(collection_get { collection="choking_e_order" index -> (integer_tmp)})
      )}
      : set_string var="string_tmp" {(
         collection_get { collection -> (collection_get { collection="choking_e_choices" index -> (choking_e_form)}) index -> (integer_tmp)}
      )}
   }
   play_choking_e_puzzle : subroutine {
      : collection_sort collection="choking_e_order" algorithm="shuffle";
      : collection_sort collection="rescue_people" algorithm="shuffle";
      : set_string var="rescue_person_1" {(collection_get { collection="rescue_people" index -> (0)} )};
      : set_string var="rescue_person_2" {(collection_get { collection="rescue_people" index -> (1)} )};
      : set_string var="rescue_person_3" {(collection_get { collection="rescue_people" index -> (2)} )};
      : set_string var="rescue_person_4" {(collection_get { collection="rescue_people" index -> (3)} )};
      : set_string var="rescue_person_5" {(collection_get { collection="rescue_people" index -> (4)} )};
      : set_string var="rescue_person_6" {(collection_get { collection="rescue_people" index -> (5)} )};
      : set_integer var="choking_e_solution" {(int(collection_get { collection="choking_e_solutions" index -> (choking_e_form)}))}
      : set_integer var="integer_tmp" value="0";
      : gosub "play_choking_e_puzzle_next_response_line";
      : add_choice  "{string_tmp}" {
         : set_integer var="integer_tmp" value="0";
         : gosub  "play_choking_e_puzzle_choice" ;
      }
      : set_integer var="integer_tmp" value="1";
      : gosub mask = "true"  "play_choking_e_puzzle_next_response_line";
      : add_choice  "{string_tmp}" {
         : set_integer var="integer_tmp" value="1";
         : gosub "play_choking_e_puzzle_choice";
      }
      : set_integer var="integer_tmp" value="2";
      : gosub "play_choking_e_puzzle_next_response_line";
      : add_choice "{string_tmp}" {
         : set_integer var="integer_tmp" value="2";
         : gosub "play_choking_e_puzzle_choice";
      }
      : set_string var="string_tmp"  {(collection_get { collection="choking_e_queries" index -> (choking_e_form)})};
      : choose "{string_tmp}" hint="numeric_list_always" ;
   }
}

######################################
#  On Describe                       #
######################################

on_describe {
   : gosub "play_music";
   : if (is_at "cc_win" ) {
      : print "CONGRATULATIONS!" ;
      : print "YOU FINISHED THE GAME! C-C YOU LATER!!!!" ;
      : press_any_key ;
      : clear_screen;
      : stop_music;
      : win_game ;
   }
  : if (is_at "puzzle_labyrinth" ) {
       : set_true "in_labyrinth";
       : press_any_key;
       : goto "labyrinth_1_1";
       : redescribe;
   }
   : else_if (is_at "labyrinth_1_1" ) {
       : set_false "has_blueprint";
       : set_integer var = "labyrinth_position_x"  value = "0" ;
       : set_integer var = "labyrinth_position_y"  value = "-13" ;
       : set_integer var = "day_spend_labyrinth_enigma" value = "0";
       : update_graphic;
   }
   : else_if (is_at "labyrinth_4_2" ) {
       : set_true "has_blueprint";
   }
   : else_if (is_at "labyrinth_5_5") {
       : if (has_blueprint) {
           : set_false "in_labyrinth";
           : set_false "is_labyrinth_open";
           : print "You did it! You're a hero, CC!";
           : create "florrie" target = "living_room";
           : print "Oh, Florrie is back!!!" ;
           : print "Why don't you <TALK<#cc0>> to her?" ;
           : set_true "is_florrie_alone_in_living_room" ;
           : set_false "is_june_in_living_room" ;
           : set_false "is_e_in_living_room" ;
           : destroy "june" ;
           : destroy "ee" ;
           # : redescribe ;
           : goto "living_room" ;
       }
       : else {
           : print "You found a way through, but missed the blueprint!";
       }
       : press_any_key;
       : goto "living_room";
       : redescribe;
   }
   : if (is_at "living_room" && in_labyrinth) {
      : print "Well, that didn't work out, but it's not an easy task. Luckily, that was a simulation that 7Cs' AI ran through her algorithms. Thanks to that we found the mistake in advance so we are safe. Let's try again.";
      : set_false "in_labyrinth";
      : press_any_key;
      : redescribe;
   }
}

######################################
#  On Command                        #
######################################

on_command {
   : match "follow _;about _; credit _; credits _; readme _; examine cosmoose; cosmoose - ; dhxp -"  {
      : if ((verb_is "about" || verb_is "follow") && is_exists (s1())) {
         : set_sentence {("examine "+ rationalized "noun1")};
      }
      : else {
          : print "YOU CAN FOLLOW OR CHECK COSMOOSE EVERYWHERE: <cosmoose.org>[https://cosmoose.org]." ;
      }
   }
   : match "examine logo"  {
      : if (is_at "puzzle_florrie_laptop") {
          : print "YOU CAN FOLLOW OR CHECK COSMOOSE EVERYWHERE: <cosmoose.org>[https://cosmoose.org]." ;
      }
   }
   : match "get all" {
     : print "You don't have proper pockets, that's a little too much.";
   }
   : match "get toy ; get bull ; get star ; get plushie ; get boat ; get bed" {
      : if (is_at "cc_room") {
         : print "You are not a baby anymore. No need to take toys with you." ;
      }
   }

   : match "refresh _" {
      : set_sentence "look";
   }
   : match "verb noun" {
      : print "You need to type commands consisting in one verb or one verb and one noun, for example:\n\n>><EXAMINE<#cc0>>\n>><LOOK PIGEON<#cc0>>\n\nType <HELP<#cc0>> to have a summary of useful commands.";
   }
   : match "save cc; help cc" {
      : print "We will do it!";
   }
   : match "examine mirror"  {
      : if (is_at "cc_room") {
         : print "A mirror inspired by antique Venetian aesthetics. Except the usual floral details have been replaced with feathers and bird shapes." ;
      }
   }
   : match "examine star"  {
      : if (is_at "cc_room") {
         : print "You are definitely going to become the number one idol of the Cosmooverse! This star is a gentle reminder." ;
      }
   }
   : match "examine bull"  {
      : if (is_at "cc_room") {
         : print "One of the bouncy bulls you can find in the Cosmooverse." ;
      }
   }
   : match "examine bed"  {
      : if (is_at "cc_room") {
         : print "A bed that is easy to disassemble and transport, made by company IDEA." ;
      }
   }
   : match "examine porthole ; examine space"  {
      : if (is_at "cc_room" || is_at "cc_hero_june") {
         : print "A small view on the stars and planets nearby." ;
      }
      : if (is_at "living_room"|| is_at "puzzle_florrie_laptop") {
         : print "A premium view on the stars and planets nearby." ;
      }
      : if (is_at "wools_wheels") {
         : print "A nice view on the stars and planets nearby." ;
      }
      : if (is_at "crispy_garden") {
         : print "It's so sunny here." ;
      }
      : if (is_at "party_planet" || is_at "party_planet_inside") {
         : print "Not much to see here." ;
      }
   }
   : match "examine cube; examine toys"  {
      : if (is_at "cc_room") {
         : print "We all need our toys, no matter how grown up we are." ;
      }
   }
   : match "examine plushie"  {
      : if (is_at "cc_room") {
         : print "An old gift from Wool. Is it a bear, a lion? Who cares, you're too old to play with it." ;
      }
   }
   : match "examine boat"  {
      : if (is_at "cc_room") {
         : print "This boat is an old gift from RuD, a self-proclaimed space pirate who loves Earth pirates too." ;
      }
   }
   : if (is_present "map" && !is_carried "map" && is_at "cc_room") {
      : match "get map" {
        : print "You may be growing up quickly, you still have a lot to learn. This <map<#cc0>> may be useful. You take it!";
        : pocket "map";
        : print "The <map<#cc0>> has been added to your <INVENTORY<#cc0>>.";
        : press_any_key;
      }
      : match "exit _" {
         : mask {
            : print "You may be growing up quickly, you still have a lot to learn. This <map<#cc0>> may be useful. You take it!";
            : pocket "map";
            : print "The <map<#cc0>> has been added to your <INVENTORY<#cc0>>.";
            : press_any_key;

         }
      }
   }
   : match "examine map; map _; use map" {
      : if (is_carried "map") {
          : set_theme "theme_mono";
          : clear_screen;
          : print {("^n^^c^<______<#000>><________<#000>>|<CC's Bedroom<#" + (is_at "cc_room"?"afa" : "faa")+">>|")};
          : print "^n^^c^--------------------------";
          : print {("^n^^c^|<Wool's Wheels<#" + (is_at "wools_wheels"?"afa" : "faa")+">>| <Common Room<#" + (is_at "living_room"?"afa" : "faa")+">>|")};
          : print "^n^^c^--------------------------";
          : print {("^n^^c^|<Party Planet<#" + (is_at "party_planet"?"afa" : "faa")+">><_<#000>>| <Crispy Yard<#" + (is_at "crispy_garden"?"afa" : "faa")+">>|")};
          : print "^n^^c^--------------------------";
          : print {("^n^^c^⬇<_____________<#000>>")};
          : print {("^n^^c^<Inside<#" + (is_at "party_planet_inside"?"afa" : "aaf")+">><_____________<#000>>")};

          : press_any_key;
          : set_theme "theme_base";
          : redescribe;
      }
      : else_if (is_present "map") {
         : print "A <map<#cc0>> of the ship, could it be useful?";
      }
   }
   : match "sound _; music _; volume _" {
      : if (preposition_is "on" || noun1_is "lowest" || noun1_is "low" || noun1_is "normal" || noun1_is "high" || noun1_is "highest") {
         : mask {
            : set_string var="string_tmp" {(rationalized "noun1")}
            : set_sentence "nothing";
            : if (music_ever_enabled) {
               : if (!sysvar_bool "sysvar_sound_enabled") {
                  : set_sentence "sound on";
                  : set_false "is_sound_muted";
               }
            }
            : else {
               : gosub "set_sound_on";
               : if (music_ever_enabled) {
                  : gosub "play_music";
               }
               : else {
                  : set_string var="nothing_message" value="You have not enabled the music";
               }
            }
            : if (string_tmp != "") {
               : set_string var="sound_volume" {(string_tmp)};
               : print "Sound volume set to {sound_volume}.";
            }
            : gosub "update_volume";
         }
      }
      : else_if(noun1_is "off") {
         : mask {
           : set_sentence "sound off";
           # to prevent not restarting on SOUND ON
           : play_music "music_win";
           : set_string var = "current_track" value="";
           : set_true "is_sound_muted";
           : gosub "update_volume";
         }
      }
      : else {
         : if (sysvar_bool "sysvar_sound_enabled") {
            : print "<SOUND OFF<#cc0>> | <LOWEST<#cc0>> | <LOW<#cc0>> | <NORMAL<#cc0>> | <HIGH<#cc0>> | <HIGHEST<#cc0>> to disable sound or change volume.";
         }
         : else {
            : print "<SOUND ON<#cc0>> | <LOWEST<#cc0>> | <LOW<#cc0>> | <NORMAL<#cc0>> | <HIGH<#cc0>> | <HIGHEST<#cc0>> to enable sound and change volume.";
         }
     }
   }
   : match "talk mirror; talk -"   {
      : if (is_at "cc_room" && !talked_to_mirror_and_solved_puzzles) {
         : print "The mirror says:" ;
         : print "\'To answer your question: " ;
         : print "Will you become the most famous superstar of the Cosmooverse?" ;
         : press_any_key ;
         : clear_screen;
         : print "No, the most famous superstar of the <Cosmooverse<#cc0>> will be a <pigeon<#cc0>>. Either musician <OK Feather<#cc0>> or bling foodie traveller <E<#cc0>>." ;
         : print "That being said, I'm just a talking mirror, whereas you are the true master of your destiny. Follow your dreams or die trying! That's the lesson I learned from <SnowWhite<#cc0>>." ;
         : print "Wow I'm tired after talking so much, I'll take a break. You can replay my previous answer any time. Ciao!\'" ;
         : press_any_key ;
         : clear_screen;
         : print "You wonder: Why pigeons though? You need to investigate and make sure to become the most famous idol of the Cosmooverse! Let's go." ;
         : press_any_key ;
         : redescribe;
      }
      : else_if (is_at "cc_room" && talked_to_mirror_and_solved_puzzles) {
         : print "The mirror says:" ;
         : print "\'The stars are watching you CC. You crave security and stability. " ;
         : print "This is exactly what you will get this month. " ;
         : print "Your career graph looks as promising as your deepest wishes, but beware of boredom. " ;
         : print "What? This wasn't your question? Well, this is my answer anyways. I feel exhausted, I'm taking a break. \'" ;
         : print "A study shows that kids get bored of a new toy after a month, but you are already tired of this mirror." ;
      }
   }
   : match "examine cosmooverse"   {
      : print "The universe where you live." ;
   }
   : match "examine ok"   {
      : print "YOU CAN FOLLOW OR CHECK OK FEATHER EVERYWHERE: <okfeather.com>[https://www.okfeather.com]." ;
   }
   : match "examine e"   {
      : print "E is a pigeon influencer. A good friend of June." ;
   }
   : match "examine florrie"  {
       : print "Florrie is an AI programmer, she is currently busy preparing for a <Pyghon<#cc0>> programming certification." ;
   }
   : match "get florrie" {
      : if (is_present "florrie") {
         : print "Florrie is nice, but she is not a thing you can take." ;
      }
   }
   : match "examine pyghon"  {
       : print "One of the most popular programming languages, along with Lava, Rudy, Purrl, E and CC+ (your favorite)." ;
   }
   : match "examine june"   {
      : print "June is your inventor friend. She also makes a lot of music." ;
   }
   : match "get june" {
      : if (is_present "june" || is_present "june_kitchen") {
         : print "You try to catch june, but now she is the one running after you with a toy pistol." ;
      }
   }
   : match "examine pigeon; examine dove"   {
      : print "Pigeons are wonderful creatures. If you are not convinced yet, check out how they saved Christmas here: <okfeather.itch.io/feathery-christmas>[https://okfeather.itch.io/feathery-christmas]." ;
   }
   : match "get pigeon; get e" {
      : if (is_at "wools_wheels") {
         : print "You are not a baby anymore. No need to take toys with you." ;
      }
      : else_if (is_present "ee" || is_present "ee_kitchen") {
         : print "E may do some \'Meet and Greet\' from time to time, but he doesn't like to be touched." ;
      }
   }
   : match "examine snow"   {
      : print "Everyone in the Cosmooverse knows the story of SnowWhite. The shared destiny of two women wth seemingly nothing in common. One had unlimited beauty and ambition, while the other became a major figure of survivalism after pulling through a severe case of food poisoning." ;
   }
   : match "kill *" {
      : if (is_carried (s1())) {
         : print {("It would be a waste to "+original "verb" + " what I have taken!!!")};
      }
      : else_if (is_present (s1())) {
         : print "The thought could never even start to cross my mind.";
      }
      : else {
        : print "Violence is never the solution.";
      }
   }
   : match "examine comment" {
      : set_sentence "comment";
   }
   : match "comment _" {
      : set_string var="string_tmp" {(substring {offset -> (strlen {(original "verb")}) string -> (command_text())})};
      : if (string_tmp == "") {
        : if (comments == "") {
            : print "I have not made any <comment<#cc0>> yet";
        }
        : else {
            : print "Here are all the comments I have made:{comments}";
        }
     }
     : else {
        : print "I'm adding that to my list of <comments<#cc0>>.";
        : set_string var="comments" {(comments + "\n<_<#000>>" + string_tmp)};
     }

   }
   : match "drop *" {
      : print "You don't need to drop anything.";
   }
   : match "cheat *" {
      : print "Unknown verb - \"cheat\".";
   }

   : match "examine puzzle" {
      : if (is_at "puzzle_florrie_laptop") {
        : print "The screen displays:" ;
        : print "Password = answer to this riddle: " ;
        : print "\'My feathers take me anywhere. The tones of grey in my plumage are reminiscent of everyone's favorite weather." ;
        : print "I am a star in the city, where admirers fawn over my shiny iridescent neck." ;
        : print "I brighten everyone's day by leaving white traces of my visit on cars and fresh air-dried laundry." ;
        : print "Who am I? UNLOCK me!\' " ;
      }
      : else_if (is_at "puzzle_hat" && !is_puzzle_wool_solved) {
        : print "An albatross, a bald eagle and a chicken each wear a hat. They see the colors of the others’ hats but not their own. " ;
        : print "Their hats were randomly drawn among 5 hats, 3 RED ones and 2 BLUE ones. " ;
        : print "The albatross is asked to guess its hat color, but responds \'No idea\'. The bald eagle cannot guess its color either." ;
        : print "Then the chicken says \'I know my color\'. Which one is it? Type GUESS color." ;
      }
      : else {
        : print "Puzzles strengthen connections between brain cells, improves short-term memory and mental speed.";
      }
   }

   : match "clear -; redescribe -" {
        : redescribe;
   }

   : match "examine laptop; read laptop; use laptop"  {
      : if (is_at "living_room" && !is_laptop_unlocked) {
         : print "This laptop belongs to <Florrie<#cc0>>." ;
         : goto "puzzle_florrie_laptop";
      }
      : else_if (is_at "living_room" && is_laptop_unlocked) {
         : print "You swore not to touch Florrie's laptop ever again." ;
      }
      : if (is_at "puzzle_florrie_laptop") {
        : print "The screen displays:" ;
        : print "Password = answer to this riddle: " ;
        : print "\'My feathers take me anywhere. The tones of grey in my plumage are reminiscent of everyone's favorite weather." ;
        : print "I am a star in the city, where admirers fawn over my shiny iridescent neck." ;
        : print "I brighten everyone's day by leaving white traces of my visit on cars and fresh air-dried laundry." ;
        : print "Who am I? UNLOCK me!\' " ;
      }
      : else_if (is_at "party_planet_inside") {
         : print "The computer seems a bit sticky with all the spilled energy drink." ;
      }
      : else_if (is_at "living_room" && !is_exists "laptop") {
        : print "Florrie must have taken her laptop." ;
      }
   }

   : match "examine tree"  {
      : if (is_at "living_room" || is_at "puzzle_florrie_laptop") {
        : print "How can so many trees grow indoors?" ;
      }
   }
   : match "get tree" {
      : if (is_at "living_room" || is_at "puzzle_florrie_laptop") {
         : print "These trees won't come off the ground." ;
      }
   }
   : match "examine bench; examine table; examine chair "  {
      : if (is_at "living_room"|| is_at "puzzle_florrie_laptop") {
        : print "The perfect place to work, chat and drink coffee." ;
      }
   }
   : match "get bench; get table; get chair" {
      : if (is_at "living_room" || is_at "puzzle_florrie_laptop") {
         : print "You would probably need assistance to move the furniture." ;
      }
   }
   : match "examine screen; read screen; read information; examine information"  {
      : if (is_at "living_room" && !is_labyrinth_open ) {
        : print "This screen sometimes displays important information. Not now, for sure." ;
      }
      : if (is_at "living_room" && is_labyrinth_open ) {
        : print "The screen shows imminent danger. Who will save us? Enter the command system!" ;
      }
      : if (is_at "puzzle_florrie_laptop") {
        : print "This laptop may be expensive, but you mainly notice the bread and chocolate crumbs stuck inside the keyboard. Florrie is a gourmet." ;
        : print "The screen displays:" ;
        : print "Password = answer to this riddle: " ;
        : print "\'My feathers take me anywhere. The tones of grey in my plumage are reminiscent of everyone's favorite weather." ;
        : print "I am a star in the city, where admirers fawn over my shiny iridescent neck.";
        : print "I brighten everyone's day by leaving white traces of my visit on cars and fresh air-dried laundry." ;
        : print "Who am I? UNLOCK me!\' " ;
      }
      : if (is_at "party_planet_inside" ) {
        : print "The screen displays what looks like a space game." ;
      }
   }
   : match "get screen" {
      : if (is_at "living_room" || is_at "puzzle_florrie_laptop" || is_at "party_planet_inside") {
         : print "It probably shouldn't be moved." ;
      }
   }

   : match "unlock pigeon; pigeon -"  {
      : if (is_at "puzzle_florrie_laptop") {
        : print "How can Florrie need clues to remember such an easy password??? " ;
        : print "You wish you had never seen this... Florrie's browser history is nothing but..." ;
        : print "PIGEONS!!!" ;
        : print "Video \'TEQ Talk: Improve Office Efficiency\' shows a pigeon at a desk drinking a protein-packed cheese beverage, and then there's \'Learn Programming in Pyghon with E\'... " ;
        : print "How can you compete? So unfair!!! You quickly lock the computer and swear not to touch it ever again." ;
        : press_any_key;
        : set_true "is_laptop_unlocked" ;
        : goto "living_room";
        : redescribe;
      }
    }

    : match "unlock dove"  {
      : if (is_at "puzzle_florrie_laptop") {
        : print "Almost there, too bad there can only be one password." ;
        : press_any_key;
      }
    }

    : match "unlock - "  {
      : if (is_at "puzzle_florrie_laptop") {
        : print "Try again." ;
        : press_any_key;
      }
    }

    : match "examine penguin"  {
      : if (is_at "puzzle_florrie_laptop") {
        : print "This logo was inspired by a scientist contracting penguinitis after a penguin bite. The most notable symptom is unconditional love towards penguins. Similar to pigeonitis." ;
      }
    }

   : match "get laptop"  {
      : if (is_at "living_room" || is_at "puzzle_florrie_laptop") {
        : print "This is not your <laptop<#cc0>>. <Florrie<#cc0>> should need it soon." ;
      }
   }

   : match "talk wool; talk - ; puzzle -"   {
      : if (is_at "wools_wheels" && !is_puzzle_wool_solved) {
         : print "You ask Wool about pigeons. He replies: \'Why are pigeons so popular?" ;
         : print "I don't know, as I am not interested in physical entities. Only logic moves me.\'" ;
         : goto "puzzle_hat" ;
         : redescribe ;
      }
      : if (is_at "puzzle_hat") {
         : print "An albatross, a bald eagle and a chicken each wear a hat. They see the colors of the others’ hats but not their own. " ;
         : print "Their hats were randomly drawn among 5 hats, 3 RED ones and 2 BLUE ones. " ;
         : print "The albatross is asked to guess its hat color, but responds \'No idea\'. The bald eagle cannot guess its color either." ;
         : print "Then the chicken says \'I know my color\'. Which one is it? Type <GUESS<#cc0>> color." ;
      }
      : if (is_at "wools_wheels" && is_puzzle_wool_solved && is_wool_wheeled) {
         : print "Wool says: \'Alright, I'll continue my alternate proof of <Wooluum<#cc0>> Hypothesis' independence from ZFC. Isn't logic beautiful?\'" ;
         : print "You have no idea what this is about but it sounds boring." ;
      }
      : if (is_at "wools_wheels" && !is_wool_wheeled) {
        : print "Finally Wool might be getting some fresh air elsewhere." ;
      }
      : else_if ((is_present "wool" || is_present "wool_kitchen") && (is_florrie_in_living_room_with_cake || is_everybody_here)) {
         : print "Wool says: \'One thing you might observe after each birthday is the loss of collagen and its change from ordered bundles of fibres to fragmented and messy fibres." ;
         : print "One consequence is that wound healing doesn't work as well.\'" ;
         : print "Err... Thanks for wishing a happy birthday I guess???" ;
         : set_true "talked_to_someone_before_cake" ;
      }
   }

   : match "thank wool ; thank -" {
    : if (is_at "wools_wheels" && is_puzzle_wool_solved && is_wool_wheeled) {
         : print "Wool says: \'Alright, I'll continue my alternate proof of <Wooluum<#cc0>> Hypothesis' independence from ZFC. Isn't logic beautiful?\'" ;
         : print "You have no idea what this is about but it sounds boring." ;
    }
   }

   : match "examine wooluum; examine hypothesis; examine zfc" {
      : print "Wooluum Hypothesis states that: " ;
      : print "Party Planet has the smallest uncountable number of chicken trees." ;
      : print "Planet NL-21 has as many chicken trees as there exist real numbers." ;
      : print "The hypothesis is that Party Planet and Planet NL-21 actually have the same amount of chicken trees." ;
      : print "It's kinda hard to prove, but one can show that it's independent from Zestful Fried Chicken (ZFC) theory." ;
   }

   : match "examine wool"   {
      : print "Wool is a mysterious mechanic who can repair anything. Not very talkative, but can be a bit judgemental." ;
      : if (is_at "puzzle_hat" || is_at "wools_wheels") {
        : print "Wool can solve most problems with his brain. That's why he's often just standing there, not moving." ;
      }
   }

   : match "get wool" {
      : if (is_present "wool" || is_present "wool_kitchen") {
         : print "Wool may look like a giant plushie, but you cannot take Wool, he has feelings too." ;
      }
   }

   : match "a -; b -; c - "   {
      : if (is_at "puzzle_hat") {
        : print "A is an albatross, B is a bald eagle and C is a chicken. But we are not here to learn about the animal kingdom." ;
      }
   }

   :match "guess red ; red -"  {
      : if (is_at "puzzle_hat") {
         : print "Correct. The chicken's hat is red of course.";
         : print "I can <EXPLAIN<#cc0>> if you want.";
         : set_true "is_puzzle_wool_solved";
         : set_true "talked_to_mirror_and_solved_puzzles" ;
         : print "Alright, maybe answering riddles is one way to become famous? You should try with someone else. Let's go.";
         : goto "wools_wheels" ;
      }
   }

   :match "guess blue ; blue -"  {
      : if (is_at "puzzle_hat") {
         : print "Wool says: \'Nope.";
         : print "If you study a bit of logic (even Tarski theories should suffice), you should be able to guess the correct color next try.\'";
      }
      : else_if (is_at "puzzle_hat" && !is_puzzle_wool_solved) {
         : print "\'Nope.";
         : print "If you study a bit of logic (even Tarski theories should suffice), you should be able to guess the correct color next try.\'";
      }
   }

   :match "guess black ; black -; guess *; guess -"  {
      : if (is_at "puzzle_hat") {
         : print "\'Did you hurt your head or something?";
         : print "You only have a choice between two colors, red and blue.";
         : print "If you study a bit of logic (even Tarski theories should suffice), you should be able to guess the correct color next try.\'";
      }
      : else_if (is_at "puzzle_hat" && !is_puzzle_wool_solved) {
         : print "\'Did you hurt your head or something?";
         : print "You only have a choice between two colors, red and blue.";
         : print "If you study a bit of logic (even Tarski theories should suffice), you should be able to guess the correct color next try.\'";
      }
   }

   :match "explain _ ; explain -"  {
      : if (is_at "wools_wheels" && is_puzzle_wool_solved) {
         : print "Let's call our 3 characters A, B and C." ;
         : print "A cannot guess its own color unless both B and C are BLUE. So there must be at least one RED hat among B and C." ;
         : print "Same for B." ;
         : print "But B could guess its own color if C was BLUE and A was RED. In this case it would mean B must be RED too." ;
         : print "Since B's Big Brain cannot guess its own color, C must be RED." ;
      }
   }

   : match "examine tool ; examine pyjeon ; examine bin; examine kirby; examine liqueon; examine guneon; examine mailbox"  {
      : if (is_at "wools_wheels") {
         : print "Wool likes his tools so much that he gave them names. There is mailbox Kirby, smart devices Liqueon, Guneon, Pyjeon..." ;
      }
   }
   : match "get tool ; get bin" {
      : if (is_at "wools_wheels") {
         : print "Wool cannot live without this, you should leave it here." ;
      }
   }

   : match "get sign" {
      : if (is_at "party_planet") {
         : print "It's too heavy, and it wouldn't be nice." ;
      }
   }

   : match "examine poster ; examine pigeon ; examine game"  {
      : if (is_at "party_planet_inside") {
         : print "Tweet Fighter, a classic game. There's a poster too." ;
      }
   }

   : match "examine tweet ; examine fighter"  {
      : if (is_at "party_planet_inside") {
         : print "Tweet Fighter is a classic fighting game." ;
      }
   }

   : match "get poster; get guitar; get headphones; get frog ; get joystick ; get mouse ; get keyboard; get chair" {
      : if (is_at "party_planet_inside") {
         : print "RuD will probably be very angry if someone touches his stuff." ;
      }
   }

   : match "examine door ; examine party; examine corridor; examine sign ; examine wall ; examine neon"  {
      : if (is_at "party_planet") {
         : print "There is not much here. Probably you should enter inside, or go somewhere else." ;
      }
   }

   : match "examine guitar ; examine headphones"  {
      : if (is_at "party_planet_inside") {
         : print "RuD has been learning music making since joining Windy Music Feedback." ;
         : print "It's a lovely place, you can join too by clicking this link: <windymusicfeedback.com>[https://indiemusicfeedback.com]." ;
      }
   }

   : match "examine frog"  {
      : if (is_at "party_planet_inside") {
         : print "The frog is an homage to RuD's mentor, an expert in martial arts who happens to be a frog. " ;
      }
   }

   : match "examine joystick ; examine keyboard ; examine mouse ; examine table"  {
      : if (is_at "party_planet_inside") {
         : print "The computer seems a bit sticky with all the spilled energy drink." ;
      }
      : else_if (is_at "puzzle_florrie_laptop") {
        : print "This laptop may be expensive, but you mainly notice the bread and chocolate crumbs stuck inside the keyboard. Florrie is a gourmet." ;
      }
   }
   : match "examine chair "  {
      : if (is_at "party_planet_inside") {
         : print "It's a gaming chair of course! You've seen DewDieDie try to sell some of these." ;
      }
   }

   : match "examine dewdiedie "  {
      : if (is_at "party_planet_inside") {
         : print "A popular gaming expert." ;
      }
   }

   : match "talk rud; talk -"  {
      : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : set_true "talked_to_rud" ;
         : print "RuD greets you warmly: \'CC!!! You look so cute today!!!!\', then begins to whisper:" ;
         : print "\'I'm in great trouble CC. I made a bet with Wool that I'd reach the Diamond Rank in Tweet Fighter before the season ends, but the deadline is in 1 hour!" ;
         : print "Wool is so sneaky, he invited me to this <Windy Music Feedback>[https://indiemusicfeedback.com] club, so I spent the whole time making songs instead of gaming! Not fair!" ;
         : print "I can do a mini-game to catch up, but I can only choose one!!! And after a few hours some of them stop working!" ;
         : print "Can you help me choose? I only have 10 cans left of RedGull, but I'll let you take one!\'" ;
         : print "He takes a sip of energy drink." ;
         : print "Can you <ASSIST RuD<#cc0>>?" ;
         : set_true "is_puzzle_rud_in_progress" ;
         : press_any_key ;
         : redescribe ;
      }
      : else_if (is_present "rud_happy" && is_at "party_planet_inside") {
         : set_true "talked_to_rud" ;
         : print "RuD is very focused, and doesn't want to be disturbed." ;
         : print "He takes a sip of energy drink." ;
      }
      : else_if ((is_everybody_here || is_florrie_in_living_room_with_cake) && is_at "party_planet_inside") {
        : print "RuD followed you to the gaming room." ;
        : print "It's probably because he wants to guard his energy drink reserve." ;
      }
      : else_if ((is_present "rud_happy"|| is_present "rud_kitchen")  && (is_florrie_in_living_room_with_cake || is_everybody_here)) {
             : print "RuD sings: \'Happy birthday CC!";
             : print "If you're happy and you know it, clap your hands, clap your hands!\'";
             : print "He takes a sip of energy drink." ;
             : set_true "talked_to_someone_before_cake" ;
      }
   }

   : match "examine bet"  {
       : if (talked_to_rud) {
         : print "RuD and Wool often make bets. This time, if RuD wins:";
         : print "Wool will repair RuD's broken and rusty umbrella drone. It has a built-in energy drink case (protecting it from shocks and leaks).";
         : print "If Wool wins:";
         : print "Wool will keep RuD's umbrella drone and turn it into a device that makes one float in the air, and protects from attacks and falling objects." ;
       }
   }

   : match "examine umbrella; examine drone"  {
       : if (talked_to_rud) {
         : print "RuD's umbrella drone is buried under a pile of old junk.";
         : print "It's a hands-free umbrella that can protect from the rain, but makes a lot of noise.";
       }
   }

   : match "examine rud"  {
       : if ((is_everybody_here || is_florrie_in_living_room_with_cake) && is_at "party_planet_inside") {
        : print "RuD followed you to the gaming room." ;
        : print "It's probably because he wants to guard his energy drink deposit." ;
       }
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "RuD looks a bit sadder than usual. A future idol could surely cheer him up!" ;
         : print "He takes a sip of energy drink." ;
       }
       : else_if (is_present "rud_happy" && is_at "party_planet_inside") {
         : print "RuD is always working hard to beat his highest score." ;
         : print "He takes a sip of energy drink." ;
       }
       : else {
         : print "RuD is a pilot and occasional postman, very skilled but he can be lazy." ;
       }

   }

   : match "get rud; get drink" {
      : if (is_present "rud_happy" || is_present "rud_sad" || is_present "rud_kitchen") {
         : print "RuD replies: \'Heeeey this is my drink! Go buy your own! \'" ;
         : print "He takes a sip of energy drink." ;
      }
   }

   : match "examine drink"  {
      : if (is_at "party_planet_inside") {
        : print "These delicious drinks are a secret mix of artificial sweeteners and flavors, high fructose corn syrup, and stimulants. " ;
        : print "They also contain amino sulfonic acids like taurine, which may reverse nerve defects in experimental diabetic neuropathic rats." ;
        : print "You should try them!" ;
      }
   }

   : match "assist rud; assist -"  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "RuD says: \'I have 1 hour left to gain 1444 points, and I can only play 1 mini-game among these three: " ;
         : print "1 - AKAFeather, a cursive writing game. You can earn 80 points per minute, but it stops working after 15 minutes, and you need to wait 43 minutes to start again." ;
         : print "2 - Brooms, a cleaning game. You can earn 26 points per minute, but it stops working after 54 minutes, and you need to wait 5 minutes to start again." ;
         : print "3 - Cosmoogang, a space cow farming game. You can earn 97 points per minute, but it stops working after 10 minutes, and you need to wait 20 minutes to start again." ;
         : print "Can you <GUESS<#cc0>> GAMENAME, for the game I should focus on?\'. " ;
         : print "He takes a sip of energy drink." ;
       }
   }

      : match "guess c; guess cosmoogang; guess 3 ; c - ; cosmoogang - ; 3 - "  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : create "energy_drink";
         : pocket "energy_drink";
         : print "RuD rushes to play Cosmoogang. Looks like he will win his bet! He looks so happy now. " ;
         : print "He takes a sip of energy drink." ;
         : print "But he doesn't look too impressed by your deduction skills." ;
         : print "He gives you a can of energy drink, as promised. " ;
         : print "It's in your <INVENTORY<#cc0>>, you can <GIVE<#cc0>>, <USE<#cc0>> it." ;
         : set_false "is_rud_sad";
         : set_false "is_puzzle_rud_in_progress" ;
         : destroy "rud_sad";
         : create "rud_happy";
         : set_true "is_seven_seas_crispy" ;
         : create "seven_seas" target = "crispy_garden" ;
       }
   }

   : match "guess -"  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print " RuD doesn't look too impressed by your deduction skills. \'Are you sure?\' he asks." ;
         : print " He takes a sip of energy drink." ;
       }
   }

   : match "guess a; guess akafeather; guess 1 ; 1 - ; akafeather -"  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "You are about to suggest \'AKAFeather\', but notice a drawing pad is required to play. RuD excels with any controller... Except this one." ;
         : print "RuD takes a sip of energy drink." ;
       }
   }

   : match "guess b; guess brooms ; guess 2; 2 - ; brooms -"  {
       : if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "You are about to suggest \'Brooms\', but remember that RuD never cleans his room or anything, there's no way he can get Diamond with this game!" ;
         : print "RuD takes a sip of energy drink." ;
       }
   }

   : match "take road"  {
      : if (is_at "party_planet" && is_exists "rud_sad") {
         : print "You can't right now, it's too early.";
      }
   }

   : match "give laptop"  {
      : if (is_at "living_room") {
         : print "This is not your <laptop<#cc0>>." ;
      }
   }

   : match "give drink "  {
      : if ((is_present "rud_happy" || is_present "rud_kitchen") && is_carried "energy_drink") {
         : print "RuD says: \'Look I promised that if you helped I'd give you this can. It already hurts me to see it go, so please keep it out of my sight.\'" ;
         : print "He takes a sip of his own energy drink." ;
      }
      : if ((is_present "seven_seas" || is_present "sevenseas_kitchen") && is_carried "energy_drink") {
         : print "Seven Seas says: \'I really appreciate your gesture but I think I am allergic to one component of this drink." ;
         : print "Last time I tried it, I began shaking everywhere and lost all control of my movements." ;
         : print "It might contain too much taurine.\'" ;
      }
      : if ((is_present "june"|| is_present "june_kitchen") && is_carried "energy_drink") {
         : print "June says: \'Oh it's the juice that looks like Clayrox toilet cleaner! Yeah tastes great, I don't want to deprive you of it!\'" ;
      }
      : if ((is_present "wool"|| is_present "wool_kitchen") && is_carried "energy_drink") {
         : print "Wool says: \'No thanks. People think energy drinks boosts your attention, but it's all a lie.\'" ;
      }
      : if ((is_present "ee" || is_present "ee_kitchen") && is_carried "energy_drink") {
         : print "E says: \'When I tried this, my disappointment was immeasurable and my day was ruined.\'" ;
      }
      : if (is_present "florrie" && is_carried "energy_drink" && !has_florrie_drank) {
         : print "Florrie takes a sip of energy drink..." ;
         : press_any_key ;
         : print "She coughs for a while, then disappears for a minute, then comes back and whispers:" ;
         : print "\'Thanks CC, it was so thoughtful of you!" ;
         : print "Please do not think I am being ungrateful, I truly cherish your gesture." ;
         : print "Maybe I am coughing because of post-exam stress? " ;
         : print "It could have affected my sense of taste... Look CC, the only thing that matters is what you think. " ;
         : print "Don't put too much focus on other people's opinions. " ;
         : print "If you enjoy this drink, then more power and energy to you!\' " ;
         : set_true "has_florrie_drank" ;
      }
      : else_if (is_present "florrie" && is_carried "energy_drink" && has_florrie_drank) {
         : print "Florrie says she hears her phone ringing, and excuses herself." ;
      }
   }

   : match "drink drink; drink -"  {
      : if (is_carried "energy_drink") {
         : print "You take a sip of energy drink." ;
         : print "It reminds you of cough syrup, weird but a bit addictive." ;
      }
      : else {
         : print "You think about taking a sip of energy drink." ;
      }
   }

   : match "discard drink"  {
      : if (is_carried "energy_drink") {
         : print "Please honor the present that was given to you." ;
      }
   }

   : match "discard chicken"  {
      : if (is_carried "chicken_wing") {
         : print "You may need it if you get hungry at night." ;
      }
   }

   : match "discard spherificator"  {
      : if (is_carried "spherificator" && !is_at "wools_wheels") {
         : print "If Wool finds it away, he may be more careful next time. Better keep it." ;
      }
      : if (is_carried "spherificator" && is_at "wools_wheels") {
         : print "Better put it back in its place so that Wool doesn't notice its disappearance." ;
      }
   }

   : match "examine leaderboard; examine leaders; examine leader"  {
      : if (is_at "party_planet_inside" && is_exists "rud_sad") {
        : print "RuD is so close to the top!!!! Only beaten by a certain OK.F, who must be a nerd." ;
      }
   }

   : match "show compassion"  {
      : if (is_at "party_planet_inside") {
         : print "RuD looks at you with glassy eyes and takes a sip of energy drink." ;
      }
   }

   : match "get chicken; taste chicken; grab chicken; seize chicken; eat chicken"  {
    : if (is_at "crispy_garden" && is_present "chicken_wing" && !is_carried "chicken_wing") {
        : print "You take a chicken fruit from the tree. It's delicious, full of vitamins and grown without pesticides." ;
        : pocket "chicken_wing" ;
        : print "This <WING<#cc0>> is now in your <INVENTORY<#cc0>>." ;
    }
    : else_if (is_at "crispy_garden") {
        : print "These fruits from the tree are delicious, full of vitamins and grown without pesticides." ;
    }
    : else {
        : print "The Crispy Yard has some crispy chicken on the trees." ;
    }
   }

   : match "get spherificator; take spherificator"  {
    : if (is_at "wools_wheels" && is_present "spherificator" && !is_carried "spherificator") {
        : print "You sneakily take the Banach-Spherificator. You don't know how to use it though." ;
        : pocket "spherificator" ;
        : print "This <SPHERIFICATOR<#cc0>> is now in your <INVENTORY<#cc0>>." ;
    }
    : else_if (is_at "wools_wheels") {
        : print "Wool has only one Banach-Spherificator, he takes good care of his tools." ;
    }
    : else {
        : print "Only Wool uses this kind of tool." ;
    }
   }

   : match "give chicken"  {
       : if (is_carried "chicken_wing") {
         : if ((is_present "rud_happy" || is_present "rud_kitchen")) {
            : print "RuD says: \'Thanks, but I don't want to get my fingers dirty while gaming.\'" ;
            : print "He takes a sip of energy drink." ;
         }
      : if ((is_present "seven_seas" || is_present "sevenseas_kitchen")) {
         : print "Seven Seas says: \'Interesting, as this contains vitamin C and carotenoids, it is considered a CC-rich food." ;
         : print "Certainly nutritious for you, but not as useful for me.\'" ;
      }
      : if ((is_present "june"|| is_present "june_kitchen")) {
         : print "June says: \'Thanks, but I grow my own in my lab and they are way tastier. Please try them some time!\'" ;
      }
      : if ((is_present "wool"|| is_present "wool_kitchen")) {
         : print "Wool says: \'Thanks, but my flying tool friend Pyjeon can go and grab some for me anytime.\'" ;
      }
      : if ((is_present "ee" || is_present "ee_kitchen")) {
         : print "E takes a bite of the chicken wing." ;
         : print "E says: \'Not too bad, but I prefer the ones that June give me.\'" ;
      }
      : if (is_present "florrie") {
         : print "Florrie takes a bite of the chicken wing..." ;
         : print "Florrie says: \'Thanks CC, it tastes super good! \'" ;
      }
    }
   }
   : match "give spherificator"  {
       : if (is_carried "spherificator") {
         : if ((is_present "rud_happy" || is_present "rud_kitchen")) {
            : print "RuD says: \'I've seen wool guarding it like a treasure, how did you get it? Hey I'll trade an energy drink for it!\'" ;
            : print "You politely decline, while he takes a sip of energy drink." ;
         }
         : if ((is_present "seven_seas" || is_present "sevenseas_kitchen")) {
             : print "Seven Seas says: \'Interesting, is it some kind of measuring device?" ;
             : print "My data does not contain any usage manual. How unfortunate.\'" ;
         }
         : if ((is_present "june"|| is_present "june_kitchen")) {
             : print "June says: \'No thanks, I don't understand why he uses such primitive tools." ;
             : print "I can measure everything perfectly with my gyro-bubble gum device!\'" ;
         }
         : if ((is_present "wool" || is_present "wool_kitchen")) {
             : print "Wool is deep in thoughts, he does not even notice you." ;
         }
         : if ((is_present "ee" || is_present "ee_kitchen")) {
             : print "E looks puzzled, like chicken in front of a knife." ;
         }
         : if (is_present "florrie") {
             : print "Florrie says: \'Thanks, but I would prefer some chicken wing or chocolate!\'" ;
         }
       }
   }

   : match "get mirror ; get chicken_tree"  {
      : if (is_present "mirror") {
        : print "Wow, you really have strong muscles. Thankfully this mirror is very sturdy.";
      }
      : if (is_present "chicken_tree") {
        : print "Wow, you really have strong muscles. Thankfully these trees grow back very fast." ;
      }
   }


   : match "examine garden; examine greenhouse; examine shrub ; examine glass"  {
      : if (is_at "crispy_garden") {
        : print "Everyone loves chicken trees here. The sweet smell of wings, drums and flats evokes summertime." ;
      }
   }

   : match "examine chicken; examine tree"  {
      : if (is_carried "chicken_wing") {
        : print "Fruits from the chicken tree are delicious, full of vitamins and grown without pesticides." ;
      }
      : else_if (is_at "crispy_garden") {
        : print "Everyone loves chicken trees here. The sweet smell of wings, drums and flats evokes summertime." ;
      }
   }

   : match "examine plant ; examine grass"  {
      : if (is_at "crispy_garden") {
        : print "The stars of this place are the chicken trees." ;
      }
   }

   : match "examine spherificator"  {
      : if (is_carried "spherificator") {
        : print "" ;
      }
      : else_if (is_at "wools_wheels") {
        : print "Wool said the Banach-Spherificator is the best measuring tool around. Old but reliable." ;
      }
   }

   : match "examine seven"  {
       : if (is_at "crispy_garden") {
         : print "SevenSeas is the walking encyclopedia we all dream of living with. An AI who can analyze any set of data. She <sing<#cc0>>s during her free time." ;
       }
       : else {
         : print "SevenSeas is an AI who can analyze any set of data." ;
       }
   }

   : match "talk seven ; talk -"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && !is_puzzle_seven_seas_now && !can_leave_crispy_garden) {
         : print "You ask SevenSeas about pigeons. She replies: \'Pigeons dominating online stardom? That does not coincide with the data I have!" ;
         : print "To become a successful online entertainer, one must upload regular content with professional editing, also find a unique style and niche audience, it's a lot of work!\'" ;
         : print "Hmm... You think she sounds really dull. By the way, who's the most famous star? Let's ask SevenSeas." ;
         : print "She says: \'The most famous star is... Wait CC, are you even listening to me? You look so bored." ;
         : print "Why don't you try to find out by yourself? \'" ;
         : print "Hmm... Maybe you should <TALK<#cc0>> to her again." ;
         : set_true "is_puzzle_seven_seas_now" ;
         : press_any_key ;
         : redescribe ;
         }
         : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
             : print "\'The four current most popular online hosts are:" ;
             : print "\'beats to mow the lawn to\', a music DJ" ;
             : print "\'C-Cherries\', introducing movie trailers" ;
             : print "\'DewDieDie\', a video game expert" ;
             : print "\'8k-Minute Recipes\', a cooking enthusiast." ;
             : print "Let's call them B, C, D and E. 3 of them are equally popular, but there is only 1 number one!" ;
             : print "I'll give you a <CLUE<#cc0>> for the channel you want, can you then <GUESS<#cc0>> who is the most popular?\'" ;
             : print "Use \'<CLUE<#cc0>> channel\' whenever you are ready for SevenSeas to give a clue." ;
             : print "If you know the answer, \'<GUESS<#cc0>> channel\' whenever you are ready." ;
         }
         : if (is_at "crispy_garden" && is_exists "seven_seas" && !is_puzzle_seven_seas_now && can_leave_crispy_garden) {
             : print "You are becoming a puzzle master!" ;
         }
         : else_if ((is_present "seven_seas" || is_present "sevenseas_kitchen") && (is_florrie_in_living_room_with_cake || is_everybody_here)) {
             : print "SevenSeas sings: \'Please don't let it define ya";
             : print "We were all made to die-ya";
             : print "Like worker bees, like compost bags" ;
             : print "That makes you want to feel alive, yeah\'" ;
             : print "Well... You had no idea SevenSeas could be so heavy-hearted. You will definitely throw her a huge party one of these days." ;
             : set_true "talked_to_someone_before_cake" ;
      }
   }

   : match "clue b ; clue beat ; clue - ; ask clue"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
            : print "\'beats to mow the lawn to\' is as popular as \'DewDieDie\'." ;
       }
   }

   : match "get seven" {
      : if (is_present "seven_seas" || is_present "sevenseas_kitchen") {
         : print "SevenSeas starts singing: \'Please please release me, let me go... \', then takes a more menacing tone" ;
         : print "\'Stop it or I'll sing The Daphne Song! \'" ;
      }
   }

   : match "examine daphne" {
         : print "\'The Daphne Song\' is a lullaby from \'The Guy Who Sings Your Name Over and Over\'." ;
   }

   : match "clue c; clue cherries; clue cherry; clue ccherries"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
            : print "I love \'C-Cherries\', they really use cutting edge cinema technology." ;
       }
   }

   : match "clue d; clue dew; clue dewdiedie"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
            : print "\'DewDieDie\' is as popular as \'beats to mow the lawn to\'." ;
       }
   }

   : match "clue e; clue eight; clue 8; clue 8k; clue eightk; clue eightkminuterecipes"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
            : print "I love \'8k-Minute Recipes\'!!! They have a great cheese lasagna recipe with everything made from scratch. It's as popular as \'beats to mow the lawn to\'." ;
       }
   }

   : match "guess two; guess 2; guess c; guess cherries; guess cherry; guess ccherries"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
         : print "Well-done! Indeed, C-Cherries is currently the top star." ;
         : print "C-Cherries looks very wealthy. That's probably the main explanation for this incredible success." ;
         : print "SevenSeas says: \'CC, I may have another suggestion for becoming popular." ;
         : press_any_key ;
         : print "My data shows heroes are popular. You could become one? " ;
         : print "A hero is someone who saves lives." ;
         : print "According to statistics, choking is a most common emergency. Here are the first-aid steps: " ;
         : print "1. Give 5 back blows. " ;
         : print "2. Give 5 abdominal thrusts (also known as the Heimlich maneuver)." ;
         : print "3. Alternate between 5 blows and 5 thrusts until the blockage is dislodged." ;
         : press_any_key ;
         : print "And voilà. I cannot wait to watch you experiment this technique!\'" ;
         : print "This sounds like too much information, of the boring kind. Let's scram." ;
         : set_true "can_leave_crispy_garden" ;
         : set_true "can_create_june_e" ;
         : create "june" target = "living_room";
         : create "ee" target = "living_room";
         : destroy "laptop" ;
         : set_true "is_june_in_living_room" ;
         : set_true "is_e_in_living_room" ;
         : set_false "is_puzzle_seven_seas_now" ;
         : press_any_key ;
         : redescribe ;
       }
   }

   : match "guess one; guess 1; guess three; guess 3; guess four; guess 4; guess b; guess beat; guess d; guess dew; guess dewdiedie ; guess e; guess eight; guess 8; guess 8k; guess eightk; guess eightkminuterecipes"  {
       : if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
         : print "Maybe you need another <CLUE<#cc0>>?" ;
       }
   }

   : match "sing _" {
      : if (is_present "seven_seas") {
        : print "SevenSeas starts to sing: \'I have a dream, about a place I wanna go...\' but suddenly takes a louder deeper voice and frenetically repeats \'DAPHNE DAPHNE DAPHNE...\'." ;
      }
      : else_if (is_present "rud_sad") {
        : print "RuD sings Cosmoose's hit \'Release Me\': \'If we'll make it outta here, I'll need a vacation \'" ;
        : print "He takes a sip of energy drink." ;
      }
      : else_if (is_present "rud_happy") {
        : print "RuD sings Cosmoose's hit \'I wanna conquer youuuuu yeah!\'. Who knew he had such golden pipes?" ;
        : print "He takes a sip of energy drink." ;
      }
      : else_if (is_present "wool") {
        : print "Wool sings \'Time to clap your hands, clap your hands and GO!\'. Who knew he liked such mainstream music?" ;
      }
      : else_if (is_present "florrie") {
        : print "Florrie sings \'So crispy, so happy, all the other wings now seem so mushy!\'. She must have been to the chicken tree not long ago." ;
      }
      : else_if (is_present "june" && is_present "ee") {
        : print "June and E sing OK Feather's hit \'Coocoo Cafe\'. You can listen to this song at the end of the game <Feathery Christmas>[https://okfeather.itch.io/feathery-christmas]." ;
      }
      : else_if (is_puzzle_florrie_solved) {
        : print "All the people in the room begin singing \'생일 축하합니다 지구에서 우주에서 제일 사랑합니다\'." ;
      }
      : else {
         : print "You start singing your favourite viral song, \"Cosmoose Gang\".";
      }
   }

   : match "x fresh; x air; x smell; get fresh; get air; x smell; smell _ ; breathe *; breathe -"  {
      : if (is_at "cc_room") {
         : print "Let's <EXIT<#cc0>> and get some fresh air.";
      }
      : else_if (is_at "party_planet_inside") {
         : print "The mixed smell of spilled energy drinks and crisps really makes me want to cook a proper meal.";
      }
      : else {
         : print "It's nice to get some fresh air.";
      }
   }

   : match "talk korean ; talk -"  {
    : if (is_at "living_room" && is_exists "june") {
        : print "June sings the song she wrote: \'빨리 빨리 빨리 time to start\'" ;
    }
    : else_if (is_at "living_room" && is_exists "ee") {
        : print "E sings the song June wrote: \'빨리 빨리 빨리 time to start\'" ;
    }
   }

   : match "talk june"  {
      : if (is_at "living_room" && is_exists "june" && !talked_to_e) {
          : print "You ask June how a pigeon could be more popular than you. It deserves an explanation!" ;
          : print "June replies: \'Why ask me, while you have a pigeon right here! Ask him away!" ;
      }
      : else_if (is_at "living_room" && is_exists "june" && !is_puzzle_ee_solved && !talked_to_june) {
          : set_false "is_seven_seas_crispy" ;
          : set_false "is_wool_wheeled" ;
          : destroy "seven_seas" ;
          : destroy "wool" ;
          : print "June says: \'Hey CC! Wanna see my Pigeoshop software? It transforms any face or body into a realistic pigeon!" ;
          : print "Florrie uses it all the time. Well... Might be because I hacked her laptop, but her password is so easy to remember! \'" ;
          : print "OK... That explains Florrie's pigeon browsing history. What does June have to say about the mirror's prediction?" ;
          : press_any_key ;
          : print "She replies: \'Does the mirror always tell the whole truth? Well, no machine can tell the whole truth! " ;
          : print "Imagine a mirror giving all the answers! Then the world would be forever at peace, which is definitely NOT happening now hahaha. " ;
          : print "However, what I know for sure is MY TRUTH!" ;
          : print "In my world, pigeons will always be number 1!\'" ;
            # : redescribe ;
          : press_any_key ;
          : print "You start feeling hungry. Also it's your birthday and no one said anything. Why is the world so unfair?" ;
          : print "Where is your cake??? Let's <ASK<#cc0>> June." ;
          : set_true "talked_to_june" ;
      }
      : else_if (is_at "living_room" && is_exists "june" && is_puzzle_ee_solved && !is_labyrinth_open) {
          : print "June says: \'Wow, you're a real hero! Thank you for saving E, I'm looking forward to his new videos!\'" ;
      }
      : else_if (is_at "living_room" && is_exists "june" && is_labyrinth_open) {
            : print "She is watching a video of E, giggling. Does she really enjoy that content?" ;
            : print "She seems totally unfazed by the alarm though." ;
      }
      : else_if ((is_present "june" || is_present "june_kitchen") && (is_florrie_in_living_room_with_cake || is_everybody_here)) {
         : print "June sings: \'꽃보다 더 곱게 별보다 더 밝게 사자보다 용감하게";
         : print "Happy birthday to you 깃털 같은 내 친구야 만수무강 하시라~~~~~~!!!!";
         : print "Brighter than flowers, brighter than stars, braver than lions" ;
         : print "Happy birthday to you, my feather-like friend, you shall live forever!!!!\'" ;
         : print "Beautiful. This version of the song is your favorite." ;
         : set_true "talked_to_someone_before_cake" ;
      }
   }

   : match "talk e"  {
        : if (is_at "living_room" && is_exists "ee" && !is_puzzle_ee_solved) {
              : set_false "is_seven_seas_crispy" ;
              : set_false "is_wool_wheeled" ;
              : destroy "seven_seas" ;
              : destroy "wool" ;
              : print "E says: \'Want to know the secret of my viral videos?" ;
              : print "I am passionate about what I do. Everyday, fans send me coupons to try new pizzas, and I document my journey eating pizzas. " ;
              : print "Sometimes I receive 50, even 100 coupons the same day, but I am committed to using every fresh daily coupon. I think my dedication is what brings happiness to my fans." ;
              : print "One fan gifted me this gold necklace, I will always cherish it." ;
              : press_any_key ;
              : print "It's still a real job though, you don't just do this for yourself. One subscriber called RuDenergy paid a hefty amount to see a comparison review of different energy drinks." ;
              : print "How to describe these beverages... My disappointment was immeasurable and my day was ruined. But your fans are always more important, and it was good money.\'" ;
              : set_true "talked_to_e" ;
        }
        : if (is_at "living_room" && is_exists "ee" && is_puzzle_ee_solved && !is_labyrinth_open) {
            : print "After you carefully followed SevenSeas' instruction, E collapses on the floor..." ;
            : print "..." ;
            : press_any_key ;
            : print "Suddenly, he hysterically jumps at you!!! \'Hohoho I bet that scared you!!! But I swear I wasn't faking the first time!\'" ;
            : print "What an unfunny guy. How can he be so popular, young punks these days have no taste." ;
            : print "June is also crying with laughter, while filming. She says \'This video will be viral HeroCC, great job!\'" ;
            : press_any_key ;
            : print "Oh no!!!!!! The screen is flashing and beeping!!!! What's going on?" ;
            : print "Let's <ENTER<#cc0>> the command panel to know, quick!" ;
            : set_true "is_labyrinth_open" ;
        }
        : else_if (is_at "living_room" && is_exists "ee" && is_labyrinth_open) {
            : print "\'Don't you think you have something more pressing to do than talk to me again?\'" ;
        }
        : else_if ((is_present "ee" || is_present "ee_kitchen") && (is_florrie_in_living_room_with_cake || is_everybody_here)) {
         : print "E says: \'This cake is rather small." ;
         : print "For my birthday my subscribers sent me 77 cakes. ";
         : print "I also received flour, milk and sugar to prepare for food shortage.\'" ;
         : print "Okaaaaay... Don't meet your idols, they say. " ;
         : set_true "talked_to_someone_before_cake" ;
      }
   }


   : match "examine necklace"  {
      : if (is_exists "ee") {
        : print "<E<#cc0>> always wears this bling gold necklace. Its symbol means wealth, and refers to the authorities supervising credit institutions and the stability of the financial system." ;
      }
   }

   : match "get necklace" {
      : if (is_present "ee" || is_present "ee_kitchen") {
         : print "When you become a big star you will get way nicer stuff than this tacky necklace." ;
      }
   }

   : match "ask cake ; ask june ; ask -"  {
       : if (is_at "living_room" && is_exists "june" && is_exists "ee" && !is_puzzle_ee_solved && talked_to_june && talked_to_e) {
            : print "E notices your distress and starts laughing." ;
            : print "He says: \'Hehehe, I was secretly filming you, now I know what my next viral video will be!" ;
            : print "The Cosmooverse's most notorious CRYBABY! Cry CC Cry!\'" ;
            : press_any_key ;
            : print "You are mortified. You don't want to be that kind of celebrity." ;
            : print "June gives you a small plastic thing:" ;
            : print "\'This is a magic <beak<#cc0>>, it transforms you into a superhero called HeroCC.\'" ;
            : print "As fishy as it sounds, you try it on and..." ;
            : goto "cc_hero_june" ;
            : redescribe ;
       }
   }

   : match "examine beak; examine herocc; examine hero; talk june; thank june; beak -; talk -"  {
      : if (is_at "cc_hero_june") {
        : print "This thing looks like a Belgian cuberdon, those raspberry gummy candies." ;
        : print "Wow! You feel lighter and different." ;
        : print "You hear a strangled noise." ;
        : print "E, who was eating popcorn, is starting to choke!!!!!" ;
        : press_any_key ;
        : print "June asks: \'E are you choking??? If so, make an OK sign!!\'" ;
        : print "You know well that pigeons don't make OK signs... And remember SevenSeas' wise advice." ;
        # : print "TODO puzzle number 5. TALK TO E when you see this message." ;
         : print "Some of your friends are passing by and can help you, but do you remember SevenSeas' advice?" ;
         : gosub "play_choking_e_puzzle";
         : print "Awesome! Thanks for helping save E! Now try to <TALK<#fee>> to him." ;
      }
   }
   : match "transform -; transform _" {
      : print "You wish you could transform yourself into the most famous idol. But life is hard.";
   }
   : match "cry -; cry _" {
      : print "Today is your birthday CC. Which means you are all grown up, and grown-ups don't cry unless they accidentally reboot their computer and lose unsaved files.";
   }

   : match "talk florrie ; talk -"  {
      : if (is_exists "florrie" && is_at "living_room" && !is_puzzle_florrie_solved ) {
        : print "Florrie warmly greets you and listens to your accomplishments." ;
        : print "She says: \' So now you are a hero and a puzzle master? Wonderful!!!" ;
        : print "Speaking of puzzles, I officially passed my Pyghon exam! But I almost failed because I couldn't remember my password..." ;
        : print "Also, there was this question to quickly solve! Can you try it CC? Here it is:\'" ;
        : press_any_key ;
        : set_true "is_puzzle_florrie_now" ;
        : print "The following sentence:" ;
        : print "\'Few radios play June's song.\'" ;
        : print "translates to (thanks to a mysterious mathematical formula):" ;
        : print "19-14-257" ;
        : print "157-4-10-40-109-175" ;
        : print "124-70-4-304" ;
        : print "49-214-95-14'175" ;
        : print "175-109-95-25" ;
        : press_any_key ;
        : print "Can you guess the original sentence behind the following:" ;
        : print "109-124-14-95" ;
        : print "175-109-214-157-7-14" ;
        : print "95-109-257" ;
        : print "<TYPE<#cc0>> THEFULLANSWER (without spaces)." ;
        : print "<HELP<#cc0>> can help if you are stuck. " ;
      }
      : else_if (is_present "florrie"  && (is_florrie_in_living_room_with_cake || is_everybody_here)) {
             : print "Florrie sings: \'Happy birthday CC!";
             : print "Try this cake we made with plenty of";
             : print "C6H1206 it's so sweet\'" ;
             : print "Hmm... What is she talking about?" ;
             : set_true "talked_to_someone_before_cake" ;
      }
   }

   : match "type opensourcenow ; opensourcenow -; guess opensourcenow ; write opensourcenow"  {
        : if (is_exists "florrie" && is_at "living_room" && is_puzzle_florrie_now ) {
            : print "Awesome!!! What could this mean though?" ;
            : set_false "is_puzzle_florrie_now" ;
            : set_true "is_puzzle_florrie_solved" ;
            : set_true "is_everybody_here" ;
            : set_false "is_florrie_alone_in_living_room" ;
            : print "Everybody has arrived!!!!" ;
            : print "Wool, RuD, SevenSeas, are here with June, E and Florrie!" ;
            : print "<TALK<#cc0>> to them, so you can <EAT<#cc0>> cake!" ;
            : create "wool_kitchen" ;
            : create "sevenseas_kitchen" ;
            : create "rud_kitchen" ;
            : create "june_kitchen" ;
            : create "ee_kitchen" ;
            : press_any_key ;
            : set_true "you_win" ;
            : redescribe ;
        }
   }

   : match "examine cake; examine flame; examine candle; examine birthday" {
    : if (is_florrie_in_living_room_with_cake) {
        : print "Today it's your one-year birthday. You can't wait to eat cake!" ;
    }
    : else {
        : print "You think of the famous song about the delicious and moist cake." ;
    }
   }

   : match "eat cake; get cake; get candle; ask cake; eat -" {
   : if (is_everybody_here) {
        : set_true "is_florrie_in_living_room_with_cake";
        : set_false "is_everybody_here";
        : print "Florrie adds: \'It's your birthday CC!!! Let's eat your cake!" ;
        : print "Everybody is here, we're going to the kitchen!\' Let's <GO<#cc0>> there!" ;
        : press_any_key ;
        : redescribe ;
    }
    : else_if (is_florrie_in_living_room_with_cake) {
        : print "We're going to eat cake with everybody! Let's <GO<#cc0>> to the kitchen!" ;
    }
    : else {
        : print "You think of the famous song about the delicious and moist cake." ;
    }
   }

   : match "go - ; go _" {
    : if (you_win) {
        : goto "cc_win" ;
    }
    : else {
        : print "\'If you don't know where you're going, you're probably going wrong\' - Terry Pratchett" ;
    }
   }
   : match "go kitchen ; kitchen -" {
    : if (you_win) {
        : goto "cc_win" ;
    }
   }

   : match "examine yourself; talk yourself"  {
      : if (is_hero_cc) {
        : print "You are so cute, whether it's as CC or HeroCC." ;
      }
      : else {
        : print "You are so cute. If only you could be everywhere at once so the world could see your cuteness. This is why your goal is to become a superstar." ;
      }
   }

   : match "help -" {
       : print "- Type '<I<#cc0>>' to see what you are holding";
       : print "- Type '<GET<#cc0>> <objectname<#cc0>>' to pick up an item";
       : print "- Type '<GIVE<#cc0>> <objectname<#cc0>>' to give an object to someone";
       : print "- Type '<X<#cc0>>' or '<X<#cc0>> <objectname<#cc0>>' to examine clues";
       : if (is_carried "map") {
         : print "- Type '<MAP<#cc0>>' to display map (<current location<#afa>>, <accessible<#aaf>>, <inaccessible<#faa>>)";
       }
       : print "- Type <N<#cc0>>, <S<#cc0>>, <E<#cc0>>, <W<#cc0>>, <UP<#cc0>>, <DOWN<#cc0>>, <ENTER<#cc0>>, <EXIT<#cc0>> to move";
       : print "- Type '<LOAD<#cc0>>' or  '<SAVE<#cc0>>' to load or save a game";
       : print "- Type '<HINT<#cc0>>' to get a hint on a puzzle.";
       : print "- If a HINT is not sufficient, check the solution in the PDF guide. Don't give up!";
       : print "- Type '<QUIT<#cc0>>' to restart a game";
   }

   : match "hint -" {
       : if (is_at "puzzle_florrie_laptop") {
        : print "The screen displays:" ;
        : print "Password = answer to this riddle: " ;
        : print "\'My feathers take me anywhere. The tones of grey in my plumage are reminiscent of everyone's favorite weather." ;
        : print "I am a star in the city, where admirers fawn over my shiny iridescent neck." ;
        : print "I brighten everyone's day by leaving white traces of my visit on cars and fresh air-dried laundry." ;
        : print "Who am I? UNLOCK me!\' " ;
        : print "Hint: it's a famous bird, most kids like to run after them in the streets." ;
      }
      : else_if (is_at "puzzle_hat") {
        : print "All birds will know their hat color as soon as they have enough information to do so. " ;
        : print "If they don't know, it means they do not see enough hats of the same color. " ;
        : print "Think how does the chicken could use it to deduce is own hat color. " ;
        : print "Type GUESS color when you are ready." ;
      }
      : else_if (is_present "rud_sad" && is_at "party_planet_inside") {
         : print "You simply need to multiply the time interval per number of points to know which mini-game wins more points." ;
         : print "When you're done <GUESS<#cc0>> the game name!" ;
       }
       : else_if (is_at "crispy_garden" && is_exists "seven_seas" && is_puzzle_seven_seas_now) {
             : print "Equality is transitive, so you just need to keep track of the one channel that has no equal!" ;
             : print "Just ask for more <CLUE<#cc0>>S until you know enough!" ;
             : print "When you know the answer, \'<GUESS<#cc0>> channel\' whenever you are ready." ;
         }
       : else_if (is_at "living_room" && is_exists "florrie" && is_puzzle_florrie_now) {
        : print "Letters are translated to numbers following a mathematical sequence. Can you see the pattern?" ;
        : print "D = 10" ;
        : print "E = 14" ;
        : print "F = 19" ;
        : print "G = 25" ;
        }
        : else_if (is_at "living_room" && is_exists "june" && is_exists "ee" ) {
            : print "Make sure you talk to both June and E." ;
        }
        : else_if (is_at "cc_hero_june") {
            : print "What's going on with this <beak<#cc0>>?" ;
        }
        : else_if (you_win && is_everybody_here) {
            : print "Where is your cake? <EAT CAKE<#cc0>> whenever you are ready to eat!" ;
        }
        : else_if (you_win && is_florrie_in_living_room_with_cake) {
            : print "Everybody is going to the kitchen! Let's <GO<#cc0>> there!" ;
        }
        : else {
            : print "No specific hint for now, try exploring a bit!" ;
        }


   }

   : match "examine bedroom"  {
      : if (is_at "cc_room") {
         : print "This room is so cute, just like me. Lots of toys: a bull, a plushie, a boat... And a small window on the surrounding skies." ;
      }
   }

   : match "examine - ; examine room"  {
      : if (is_at "cc_room") {
         : print "This room is so cute, just like me. Lots of toys: a bull, a plushie, a boat... And a small window on the surrounding skies." ;
      }
      : if (is_at "crispy_garden") {
         : print "It's quite warm in here." ;
         : if (is_present "seven_seas") {
            : print "Do not hesitate to <TALK<#cc0>> to SevenSeas." ;
         }
      }
      : if (is_at "puzzle_florrie_laptop") {
         : print "Florrie's laptop screen shows the <Cosmoose<#cc0>> logo and a mysterious <penguin<#cc0>>." ;
      }
      : if (is_at "living_room") {
         : print "This room has everything, from nature to technology, and what a nice view on the neighboring planets." ;
      }
      : if (is_at "living_room" && is_labyrinth_open) {
         : print "The screen is flashing brightly, and making alarming sounds. It may be time to do something, urgently.";
      }
      : if (is_at "living_room" && you_win) {
         : print "You can try to <TALK<#cc0>> with your friends.";
         : print "Whenever you are ready, you can <GO<#cc0>> and eat cake." ;
      }
      : if (is_at "party_planet") {
         : print "It may look gloomy from the outside, but some people spend their nights and day inside." ;
      }
      : if (is_at "party_planet_inside") {
         : print "RuD is almost always here. He moved his guitar and all the stuff from his own bedroom (which looks like an empty garage now) to this common gaming room." ;
         : print "He takes a sip of energy drink." ;
      }
      : if (is_at "wools_wheels") {
         : print "Wool can repair any gear, mostly with the power of his brain. He is always surrounded by lots of tools." ;
      }
      : if (is_at "puzzle_hat") {
         : print "You've never seen Wool so excited. He must love a good puzzle." ;
      }
      : if (is_at "cc_hero_june") {
            : print "What's going on with this <beak<#cc0>>?" ;
        }
      : if (in_labyrinth) {
         : print "Escape without being detected by any drone, while getting the blueprint!" ;
      }
   }

   : match "examine - ; examine _ ; talk - ; talk _" {
      : if (talked_to_someone_before_cake && is_at "living_room") {
        : print "Let's <EAT<#cc0>> cake!" ;
      }
      : else_if (talked_to_someone_before_cake && is_at "living_room" && is_florrie_in_living_room_with_cake) {
        : print "Whenever you are ready, you can <GO<#cc0>> and eat cake." ;
      }
   }
   : match "nothing -" {
      : print "{nothing_message}";
      : set_string var="nothing_message" value="";
   }
   : match "repair gear" {
      : if (is_present "wool" || is_present "wool_kitchen") {
        : print "Wool says: \'Not sure there's much left to repair. I have too much free time these days.\'";
      }
   }

   : match "examine table ; examine drawer" {
      : if (is_at "wools_wheels") {
        : print "You have no idea how to use what's in this room.";
      }
   }

   : match "play *; play -" {
      : print "It's always fun to play!";
   }

   : match "wait *; wait -" {
      : print "What are you waiting for? Life is too short to stay passive.";
   }

   : match "exit -" {
      : if (!is_puzzle_wool_solved && is_at "puzzle_hat") {
        : print "If you need help here, type <HELP<#cc0>>." ;
        : print "You don't have many possible answers though, so you should be able to guess quickly." ;
      }
   }

   : match "todo puzzle" {
      : set_true "is_playing_logic" ;
      : if (is_playing_logic) {
         : gosub "play_choking_e_puzzle";
         : print "Awesome! Thanks for helping save E! Now try to <TALK<#fee>> to him." ;
      }
   }

   : if (in_labyrinth) {
      : match "start -; in -; enter -" {
          : goto "labyrinth_1_1";
          : redescribe;
      }
      : match "e _" {
         : mask {
           : if (try_move() != "") {
              : increment "day_spend_labyrinth_enigma";
              : set_integer var = "labyrinth_position_x" {( labyrinth_position_x + 16)}
              : update_graphic;
              : pause "50" ;
           }
         }
      }
      : match "s _" {
         : mask {
           : if (try_move() != "") {
              : increment "day_spend_labyrinth_enigma";
              : set_integer var = "labyrinth_position_y" {( labyrinth_position_y + 14)}
              : update_graphic;
              : pause "50" ;
           }
         }
      }
      : match "n _" {
         : mask {
           : if (try_move() != "") {
              : increment "day_spend_labyrinth_enigma";
              : set_integer var = "labyrinth_position_y" {( labyrinth_position_y - 14)}
              : update_graphic;
              : pause "50" ;
           }
         }
      }
      : match "w _" {
         : mask {
           : if (try_move() != "") {
              : increment "day_spend_labyrinth_enigma";
              : set_integer var = "labyrinth_position_x" {( labyrinth_position_x - 16)}
              : update_graphic;
              : pause "50" ;
           }
         }
      }
   }
}

######################################
#  Assets                     #
######################################

assets {
   sounds {
      music_beat_1 : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/Beat_1.mp3" ;
      music_beat_2 : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/Beat_2_ccs_room.mp3" ;
      music_wools_wheels : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/Beat07_Wools_room.mp3" ;
      music_laptop : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/lofi2_wool.mp3" ;
      music_living_room : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/lofi3_livingroom.mp3" ;
      music_party_planet_inside : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/lofi4_partyplanet.mp3" ;
      music_party_planet_outside: sound_sample "https://cosmoose.org/adventuron_april_2022/Music/lofi8_party_planet_outside.mp3" ;
      music_crispy_garden : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/lofi10_crispy.mp3" ;
      music_labyrinth_1 : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/Beat08_puzzle.mp3" ;
      music_labyrinth_2 : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/Beat11_Puzzle.mp3" ;
      music_living_room_pigeon : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/Beat09_living_room_june_E_ganggang.mp3" ;
      music_win : sound_sample "https://cosmoose.org/adventuron_april_2022/Music/funprio.mp3" ;
   }
   graphics {
      cc_room : png "https://cosmoose.org/adventuron_april_2022/Images/cc_room.png" ;
      crispy_garden : png "https://cosmoose.org/adventuron_april_2022/Images/crispy_garden_no7Cs.png" {
        : if (is_seven_seas_crispy) {
               : overlay "crispy_garden_7Cs";
        }
        : if (is_puzzle_seven_seas_now) {
               : overlay "puzzle_channels";
        }
      } ;
      wools_wheels : png "https://cosmoose.org/adventuron_april_2022/Images/wools_wheels.png" {
        : if (is_wool_wheeled) {
               : overlay "wool";
        }
      } ;
      party_planet : png "https://cosmoose.org/adventuron_april_2022/Images/party_planet.png" ;
      party_planet_inside : png "https://cosmoose.org/adventuron_april_2022/Images/rud.png" {
        : if (is_rud_sad) {
               : overlay "rudface";
               : if (is_puzzle_rud_in_progress) {
                : overlay "puzzle_rud";
               }
        }
        : if (is_puzzle_rud_in_progress) {
               : overlay "puzzle_rud";
        }
      } ;
      rudface : png "https://cosmoose.org/adventuron_april_2022/Images/rudface.png" ;
      crispy_garden_7Cs : png "https://cosmoose.org/adventuron_april_2022/Images/crispy_garden_7Cs.png" ;
      wool : png "https://cosmoose.org/adventuron_april_2022/Images/wool.png" ;
      june : png "https://cosmoose.org/adventuron_april_2022/Images/june.png" ;
      etana : png "https://cosmoose.org/adventuron_april_2022/Images/etana.png" ;
      florrie : png "https://cosmoose.org/adventuron_april_2022/Images/florrie.png" ;
      florrie_cake : png "https://cosmoose.org/adventuron_april_2022/Images/florrie_cake.png" ;
      living_room : png "https://cosmoose.org/adventuron_april_2022/Images/common_room.png" {
        : if (is_june_in_living_room) {
               : overlay "june";
               : if (is_e_in_living_room) {
                 : overlay "etana";
               }
        }
        : else_if (is_e_in_living_room) {
               : overlay "etana";
        }
        : else_if (is_florrie_alone_in_living_room) {
               : overlay "florrie";
        }
        : else_if (is_florrie_in_living_room_with_cake) {
            : overlay "florrie_cake";
        }

        : else_if (is_everybody_here) {
               : overlay "everyone";
        }


      } ;
      everyone : png "https://cosmoose.org/adventuron_april_2022/Images/everyone.png" ;
      loading_screen : png "https://cosmoose.org/adventuron_april_2022/Images/loading.png" ;
      cc_hero_june : png "https://cosmoose.org/adventuron_april_2022/Images/cc_hero.png" ;
      cc_win : png "https://cosmoose.org/adventuron_april_2022/Images/feast.png" ;
      puzzle_florrie_laptop : png "https://cosmoose.org/adventuron_april_2022/Images/laptop.png" ;
      puzzle_channels : png "https://cosmoose.org/adventuron_april_2022/Images/puzzle_channels.png" ;
      puzzle_hat : png "https://cosmoose.org/adventuron_april_2022/Images/puzzle_hat.png" ;
      puzzle_rud : png "https://cosmoose.org/adventuron_april_2022/Images/puzzle_rud.png" ;
      puzzle_ship : png "https://cosmoose.org/adventuron_april_2022/Images/puzzle_ship.png" ;
      puzzle_labyrinth : png "https://cosmoose.org/adventuron_april_2022/Images/puzzle_labyrinth.png" {
              : overlay {
                  graphic -> ("puzzle_ship")
                  x       -> ( labyrinth_position_x )
                  y       -> ( labyrinth_position_y )
            }
         };

   }
}
